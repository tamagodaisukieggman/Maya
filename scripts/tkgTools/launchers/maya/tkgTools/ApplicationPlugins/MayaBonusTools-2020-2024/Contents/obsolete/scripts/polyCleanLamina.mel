// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================


global proc polyCleanLamina()
{

	string $selMesh[] = `ls -o -ni -sl -s -dag -type "mesh"`;
	string $sai = `stringArrayIntersector`;


	for ( $m in $selMesh ) {

// find facets that have at least one nonmanifold edge
print("\nProcessing >" + $m + "< ...\n");

		string	$q;
		string	$tmp[] = `polyInfo -nme $m`;
		string	$nme[] = `ls -fl $tmp`;
	
		string	$nmf[] = `polyInfo -ef $nme`;
	
		for ( $t in $nmf ) {
			$q += `substring $t 13 (size($t))`;
		}
		clear $tmp;
		tokenize $q $tmp;
		$tmp = sort( $tmp);
		
		clear $nmf;
		$nmf[0] = $tmp[0];
		int $i = 1;
		for ( $t in $tmp) {
			if ($t != $nmf[$i-1]) $nmf[$i++] = $t;
		}
	
print ("Found " + size($nmf) + " nonmanifold Facet(s).\n");
//print $nmf;
//print ("-----------------------\n\n");
	
	
		string $delFacets[];
		clear $delFacets;
		for ( $i=0 ; $i<size($nmf) ; $i++ ) {
			if (size($nmf[$i])) {
//print("Check Facet " + $i + " against the others..\n");
				string $q1[] = `polyInfo -fe ($m + ".f[" +$nmf[$i] + "]")`;
				$q = `substring $q1[0] 13 (size($q1[0]))`;
				tokenize $q $tmp;
	
				for ( $j=$i+1 ; $j<size($nmf) ; $j++ ) {
					if (size($nmf[$j])) {
						string $q1[] = `polyInfo -fe ($m + ".f[" +$nmf[$j] + "]")`;
						$q = `substring $q1[0] 13 (size($q1[0]))`;
						string $tmp1[];
						tokenize $q $tmp1;
						if (size($tmp) == size($tmp1)) {
							stringArrayIntersector -edit -intersect $tmp $sai;
							stringArrayIntersector -edit -intersect $tmp1 $sai;
							string $tmp2[] = `stringArrayIntersector -query $sai`;
							if (size($tmp1) == size($tmp2)) {
								$delFacets[size($delFacets)] = ($m + ".f[" +$nmf[$j] + "]");
								$nmf[$j] = "";
							}
							stringArrayIntersector -edit -reset $sai;
						}
					}
				}
			}
		}

print("Deleting " + size($delFacets) + " Facet(s).\n");
//print("Deleting these " + size($delFacets) + " Facets:\n");
//print $delFacets;
//print("----------------------------------\n");
		delete  $delFacets;
	}
	deleteUI $sai;
}
