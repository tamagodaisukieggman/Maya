// ===========================================================================
// Copyright 2021 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
// international treaties.
//
//
// bonesOnCurve.mel
// 
// select a curve to build bones along

global proc bonesOnCurve( int $numBones, 
						 int $rebuildCurve,
						 int $addSplineIK){
	
	string $curves[] = `ls -selection`;

	for ($curve in $curves){
		if ($rebuildCurve){
			rebuildCurve -ch 1 
				-replaceOriginal 1 
				-rebuildType 0 
				-endKnots 1 
				-keepRange 0 
				-keepControlPoints 0 
				-keepEndPoints 0 
				-keepTangents 0 
				-spans 0
				-degree 2 
				-tolerance 0.01 
				$curve;
		}

		select -clear;
		string $curveInfo = `pointOnCurve -constructionHistory 1 $curve`;
		setAttr ($curveInfo + ".turnOnPercentage") 1;

		int $i = 0;
		string $joints[];
		for ($i; $i<=$numBones; $i++){
			float $parameter = $i * (1.0/$numBones);
			setAttr ($curveInfo + ".parameter") $parameter;
			float $position[] = getAttr ($curveInfo + ".position");
			$joints[$i] = `joint -position $position[0] $position[1] $position[2]`;
		}
		delete $curveInfo;
		joint -edit
			-orientJoint xyz
			$joints;

		if ($addSplineIK){
			select -replace $joints[0];
			select -toggle $joints[$numBones];
			select -toggle $curve;
			ikHandle -solver ikSplineSolver 
				-createCurve false;


		}
	}
}

