//===============================================
//
// サムネイルキャプチャ
//
// Fujita Yukihiro
//
//===============================================

global proc fy_captureThumnail(string $targets[], int $w, int $h, string $path)
{
    // 現在の選択を取得
    string $sel[] = `ls -sl`;

    if(`size($targets)` == 0)
    {
        print "// キャプチャするターゲットがありません。\n";

        return;
    }

    string $i;

    // 存在しないノード名リスト
    string $notExistList[];

    for($i in $targets)
    {
        // ノードが存在しなければリストに追加
        if(`objExists $i` == 0)
        {
            stringArrayInsertAtIndex(0, $notExistList, $i);
        }
    }

    // ターゲットリストから存在しないノード名を削除
    $targets = stringArrayRemove($notExistList, $targets);

    if(`size($targets)` == 0)
    {
        print "// キャプチャするターゲットがありません。\n";

        return;
    }

    // モジュールインポート
    python("import mtk3d.maya.env.display.functions as envdisp");

    // サムネイル撮影用のウィンドウがあれば削除
    if(`window -ex WND_fy_captureThumnail`)
    {
        deleteUI  WND_fy_captureThumnail;
    }

    // サムネイル撮影用のモデルパネルがあれば削除
    if(`modelPanel -ex modelPanelThumnail`)
    {
        deleteUI -panel modelPanelThumnail;
    }

    if(`windowPref -ex WND_fy_captureThumnail`)
    {
        windowPref -remove WND_fy_captureThumnail;
    }

    // 撮影用カメラ作成
    string $cam[] = `camera -farClipPlane 100000`;

    // 画面外にキャプチャ用ウィンドウ作成（2018 以降だと、完全に画面外だと正しくキャプチャできないので画面に入れる。。。）
    // window
    //     -w $w
    //     -h $h
    //     -topLeftCorner 2000 2000
    //     WND_fy_captureThumnail;

    window
        -w $w
        -h $h
        -topLeftCorner 0 0
        WND_fy_captureThumnail;

        paneLayout;

            // モデルパネル作成
            modelPanel
                -menuBarVisible false
                -cam $cam[0]
                modelPanelThumnail;

            // モデルパネルに含まれるモデルエディタを取得
            string $modelEditor = `modelPanel -q -modelEditor modelPanelThumnail`;

            // ヘッドアップディスプレイを非表示
            // グリッド非表示
            // 既定のライト
            // スムースシェード
            // 既定のマテリアルを使用しない
            // テクスチャ表示
            modelEditor -e
                -hud false
                -grid false
                -displayLights "default"
                -displayAppearance "smoothShaded"
                -useDefaultMaterial false
                -displayTextures true
                -wireframeOnShaded true
                -activeView
                $modelEditor;

            // アイコンバーのレイアウト名を取得
            string $iconBarLayout = `modelPanel -q -barLayout modelPanelThumnail`;

            // アイコンバーを非表示
            frameLayout -e -collapse true $iconBarLayout;

    // カメラ位置をリセット
    viewSet -home $cam[0];

    // ちょっとズームイン
    setAttr ($cam[1] + ".focalLength") 50;

    // 対象モデル選択
    select -r $targets;

    // カメラにフィット
    viewFit $cam[0];

    // アイソレート表示
    isolateSelect -state 1 modelPanelThumnail;

    // 選択解除
    select -clear;

    // 現在のＢＧカラーを取得
    float $rgb[3] = `displayRGBColor -q background`;

    // グラディエントＢＧかどうか
    int $gradOn = `displayPref -q -displayGradient`;

    // デフォルトＢＧカラーにする
    displayRGBColor background 0.36 0.36 0.36;

    // ウインドウ表示
    showWindow;

    // パス
    $path = substituteAllString($path, "\\", "/");

    // 拡張子がついていなかったらつける
    if(endsWith($path, ".jpg") == 0)
    {
        $path = $path + ".jpg";
    }

    // サムネイルキャプチャ
    refresh -currentView -fileExtension "jpg" -filename $path;

    // アイソレート表示オフ
    isolateSelect -state 0 modelPanelThumnail;

    // ウインドウ削除
    deleteUI WND_fy_captureThumnail;

    // BGカラーを戻す
    displayRGBColor background $rgb[0] $rgb[1] $rgb[2];

    // グラディエント設定を戻す
    displayPref -displayGradient $gradOn;

    // カメラ削除
    delete $cam;

    // 選択を復元
    select -r $sel;
}

//fy_captureThumnail(`ls -sl`, 128, 128, "d:/test");


