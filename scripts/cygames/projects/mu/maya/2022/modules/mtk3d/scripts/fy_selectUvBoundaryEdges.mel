//===============================================
//
// UV 境界のエッジを選択する
//
// Fujita Yukihiro
//
//===============================================

global proc fy_selectUvBoundaryEdges(string $method)
{
    if($method == "option")
    {
        fy_selectUvBoundaryEdges_UI;
        
        return;
    }
    
    // 現在の選択
    string $sel[] = `ls -sl`;
    
    // 選択中のメッシュを取得
    string $selNodes[] = `ls -sl -dag -tr`;

    if(`size($selNodes)` == 0)
    {
        return;
    }
    
    string $node;
    
    string $allBorderEdges[];
    
    for($node in $selNodes)
    {
        // 全てのUV を選択（UV を取得できないノードはスキップ）
        if( catchQuiet(`select -r ($node + ".map[*]")`) )
        {
            select -cl;
            
            continue;
        }
        
        // UV シェル境界へ選択を変換
        polySelectBorderShell 1;
        
        // エッジコンポーネントに変換
        string $convertedEdges[] = `polyListComponentConversion -toEdge`;
        
        // リストを平滑化
        $convertedEdges = `ls -fl $convertedEdges`;
    
        string $edge;
        string $borderEdges[];

        for($edge in $convertedEdges)
        {
            // UV コンポーネントに変換
            string $uv[] = `polyListComponentConversion -toUV $edge`;
            
            // リストを平滑化
            $uv = `ls -fl $uv`;
            
            if(`size($uv)` > 2)
            {
                // 境界エッジリストに追加
                $borderEdges[`size($borderEdges)`] = $edge;
            }
        }

        if(`size($borderEdges)` != 0)
        {
            // UV 境界エッジをハードエッジに
            if($method == "harden" || $method == "hardenAndSelect")
            {
                    polySoftEdge -angle 0 $borderEdges;
            }
            
            // 全境界エッジリストに追加
            $allBorderEdges = stringArrayCatenate($allBorderEdges, $borderEdges);
        }
        
        // 全てのエッジを選択
        select -r ($node + ".e[*]");
        
        // シェル境界エッジへ選択を変換
        polySelectBorderShell 1;
        
        // リストを平滑化
        $borderEdges = `ls -sl -fl `;

        // 全境界エッジリストに追加
        $allBorderEdges = stringArrayCatenate($allBorderEdges, $borderEdges);
    }

    // 元の選択を復元
    select -r $sel;

    // 
    if($method == "harden")
    {
        if(`optionVar -q "fy_selectUvBoundaryEdges_doSelect"`)
        {
            if(`size($allBorderEdges)` != 0)
            {
                // エッジ選択モードに
                setSelectMode components Components;
                selectType -smp 0 -sme 1 -smf 0 -smu 0 -pv 0 -pe 1 -pf 0 -puv 0;

                select -r $allBorderEdges;
            }
        }
    }
    // UV 境界エッジを選択
    else
    {
        if(`size($allBorderEdges)` != 0)
        {
            // エッジ選択モードに
            setSelectMode components Components;
            selectType -smp 0 -sme 1 -smf 0 -smu 0 -pv 0 -pe 1 -pf 0 -puv 0;

            select -r $allBorderEdges;
        }
    }
}


//===============================================
// 設定ダイアログ作成
//===============================================
global proc fy_selectUvBoundaryEdges_UI()
{
    //すでにウィンドウがあれば終了
    if (`window -exists uWnd_fy_selectUvBoundaryEdges` == 1)
    {
        deleteUI uWnd_fy_selectUvBoundaryEdges;
        //return;
    }

    //プリファレンスに値が無ければデフォルト値を設定
    if (`optionVar -ex "fy_selectUvBoundaryEdges_doSelect"` == 0)
    {
        optionVar -iv "fy_selectUvBoundaryEdges_doSelect" 0;
    }

    window 
        -title "Harden UV Boundary Edges Options"
        -resizeToFitChildren on
        -sizeable off
        -maximizeButton off
        -toolbox on
        -menuBar on
        uWnd_fy_selectUvBoundaryEdges;

        menu -label "ヘルプ" -enable on;
            menuItem 
                -label "Harden UV Boundary Edges のヘルプ"
                -c "showHelp -absolute \"https://wisdom.cygames.jp/pages/viewpage.action?pageId=123231284\""
                -enable on
                ;

    string $form = `formLayout -w 300 -h 90`;
    
    string $cll = `columnLayout -adj on`;

    int $cbxValue = `optionVar -q "fy_selectUvBoundaryEdges_doSelect"`;

    string $cbx = `checkBox -l "実行後にUV 境界エッジを選択する" -v $cbxValue cbx_fy_selectUvBoundaryEdges`;
     
    setParent ..;

    string $b1 = `button -l "実行" -c "fy_selectUvBoundaryEdges_buttonCommand(0);"`;
    string $b2 = `button -l "適用" -c "fy_selectUvBoundaryEdges_buttonCommand(1);"`;
    string $b3 = `button -l "閉じる" -c "deleteUI uWnd_fy_selectUvBoundaryEdges;"`;

    formLayout -e
        -attachForm    $cll   "top"    16
        -attachForm    $cll   "left"   64
        -attachNone    $cll   "bottom"
        -attachForm    $cll   "right"  1

        -attachNone        $b1   "top"
        -attachForm        $b1   "left"   8
        -attachForm        $b1   "bottom" 8
        -attachPosition    $b1   "right"  4 33

        -attachNone        $b2   "top"
        -attachControl     $b2   "left"   4 $b1
        -attachForm        $b2   "bottom" 8
        -attachPosition    $b2   "right"  4 66

        -attachNone        $b3   "top"
        -attachControl     $b3   "left"   4 $b2
        -attachForm        $b3   "bottom" 8
        -attachForm        $b3   "right"  8
    $form;

    showWindow;
}

//===============================================
// 設定ダイアログ作成
//===============================================
global proc fy_selectUvBoundaryEdges_buttonCommand(int $mode)
{
    optionVar -iv "fy_selectUvBoundaryEdges_doSelect" `checkBox -q -v cbx_fy_selectUvBoundaryEdges`;

    fy_selectUvBoundaryEdges("harden");

    if($mode == 0)
    {
        deleteUI uWnd_fy_selectUvBoundaryEdges;
    }
}

