//===============================================
//
// ステップトランスフォームツール
//
// Fujita Yukihiro
//
//===============================================

///////////////////////////////////////
// UI 作成
///////////////////////////////////////
global proc shr_stepTransTool()
{
    // TA ツールログ送信
    shr_toolLog("Step Transform Tool", "1.0.0", "launched.");

    // プリファレンスに値が無ければデフォルト値を設定
    if(`optionVar -ex "shrStepTrans_selectedTab"` == 0)
    {
        // 選択しているタブのインデックス
        optionVar -iv "shrStepTrans_selectedTab" 1;

        // トランスフォームツールのマニピュレータ連動
        optionVar -iv "shrStepTrans_syncTransformManip" 0;
    }

    if (`optionVar -ex "shrStepTrans_buttonValue_a"` == 0)
    {
        // 移動値ボタンの数値
        optionVar -fv "shrStepTrans_buttonValue_a" 10;
        optionVar -fv "shrStepTrans_buttonValue_b" 5;
        optionVar -fv "shrStepTrans_buttonValue_c" 2.5;
        optionVar -fv "shrStepTrans_buttonValue_d" 1.25;
        optionVar -fv "shrStepTrans_buttonValue_e" 1;
        optionVar -fv "shrStepTrans_buttonValue_f" 0.5;
        optionVar -fv "shrStepTrans_buttonValue_g" 0.1;
        optionVar -fv "shrStepTrans_buttonValue_h" 0.01;

        // 選択している移動値ボタン
        optionVar -sv "shrStepTrans_selectedButtonName" "uBtn_a_shrStepTransTool";

        // 選択している移動値ボタンの数値
        optionVar -fv "shrStepTrans_selectedButtonValue" 10;

        // 選択している単位
        optionVar -iv "shrStepTrans_selectedUnit" 1;

        // 選択している座標軸
        optionVar -iv "shrStepTrans_selectedAxis" 3;

        // UV の保持設定
        optionVar -iv "shrStepTrans_preserveUVs" 0;

        // 1m あたりのMaya unit 数
        optionVar -fv "shrStepTrans_unitsPerMeter" 100;
    }

    if (`optionVar -ex "shrStepTrans_buttonValue_rot_a"` == 0)
    {
        // 回転値ボタンの数値
        optionVar -fv "shrStepTrans_buttonValue_rot_a" 180;
        optionVar -fv "shrStepTrans_buttonValue_rot_b" 120;
        optionVar -fv "shrStepTrans_buttonValue_rot_c" 90;
        optionVar -fv "shrStepTrans_buttonValue_rot_d" 60;
        optionVar -fv "shrStepTrans_buttonValue_rot_e" 45;
        optionVar -fv "shrStepTrans_buttonValue_rot_f" 30;
        optionVar -fv "shrStepTrans_buttonValue_rot_g" 22.5;
        optionVar -fv "shrStepTrans_buttonValue_rot_h" 15;

        // 選択している回転値ボタン
        optionVar -sv "shrStepTrans_selectedButtonName_rot" "uBtn_rot_a_shrStepTransTool";

        // 選択している回転値ボタンの数値
        optionVar -fv "shrStepTrans_selectedButtonValue_rot" 180;

        // 選択している回転中心
        optionVar -iv "shrStepTrans_selectedRotCenter" 1;

        // 選択している座標軸
        optionVar -iv "shrStepTrans_selectedAxis_rot" 1;

        // UV の保持設定
        optionVar -iv "shrStepTrans_preserveUVs_rot" 0;
    }

    if (`optionVar -ex "shrStepTrans_buttonValue_scale_a"` == 0)
    {
        // スケール値ボタンの数値
        optionVar -fv "shrStepTrans_buttonValue_scale_a" 2;
        optionVar -fv "shrStepTrans_buttonValue_scale_b" 1.5;
        optionVar -fv "shrStepTrans_buttonValue_scale_c" 1.25;
        optionVar -fv "shrStepTrans_buttonValue_scale_d" 1.1;
        optionVar -fv "shrStepTrans_buttonValue_scale_e" 0.75;
        optionVar -fv "shrStepTrans_buttonValue_scale_f" 0.5;
        optionVar -fv "shrStepTrans_buttonValue_scale_g" 0.25;
        optionVar -fv "shrStepTrans_buttonValue_scale_h" 0.1;

        // 選択しているスケール値ボタン
        optionVar -sv "shrStepTrans_selectedButtonName_scale" "uBtn_scale_a_shrStepTransTool";

        // 選択しているスケール値ボタンの数値
        optionVar -fv "shrStepTrans_selectedButtonValue_scale" 2;

        // 選択しているスケール中心
        optionVar -iv "shrStepTrans_selectedScaleCenter" 1;

        // 選択している座標軸
        optionVar -iv "shrStepTrans_selectedAxis_scale" 1;

        // UV の保持設定
        optionVar -iv "shrStepTrans_preserveUVs_scale" 0;

        // 均等スケール
        optionVar -iv "shrStepTrans_uniformScale_scale" 0;
    }

    // ウィンドウ作成
    if (`window -exists uWnd_shrStepTransTool` == 1)
    {
        deleteUI uWnd_shrStepTransTool;
    }

    window
        -title "Step Transform Tool"
        -w 100 -h 100
        -sizeable off
        -toolbox on
        -maximizeButton off
        -mb on
        -resizeToFitChildren on
        uWnd_shrStepTransTool;

        menu -l "Option";
            menuItem
                -l "単位の設定..."
                -c "layoutDialog -t \"Step Transform Tool Options\" -ui shr_stepTransTool_optionDialog"
               ;
            menuItem
                -l "座標系の指定をマニピュレータに反映する"
                -cb `optionVar -q shrStepTrans_syncTransformManip`
                -c "optionVar -iv shrStepTrans_syncTransformManip (`optionVar -q shrStepTrans_syncTransformManip`==0)"
               ;

        string $helpCmd = "showHelp -absolute \"https://wisdom.cygames.jp/pages/viewpage.action?pageId=434084734\"";

        menu -l "Help" -helpMenu on;
            menuItem
                -l "Step Transform Tool のヘルプ"
                -c $helpCmd
               ;

    tabLayout
        -selectCommand "optionVar -iv shrStepTrans_selectedTab `tabLayout -q -selectTabIndex uTbl_shrStepTransTool`;"
        uTbl_shrStepTransTool;

    // 移動タブ作成
    shr_stepTransTool_createMoveTab;

    // 回転タブ作成
    shr_stepTransTool_createRotateTab;

    // スケールタブ作成
    shr_stepTransTool_createScaleTab;

    tabLayout -e -tabLabel uCl_move_shrStepTransTool "移動" uTbl_shrStepTransTool;
    tabLayout -e -tabLabel uCl_rotate_shrStepTransTool "回転" uTbl_shrStepTransTool;
    tabLayout -e -tabLabel uCl_scale_shrStepTransTool "スケール" uTbl_shrStepTransTool;
    tabLayout -e -selectTabIndex `optionVar -q shrStepTrans_selectedTab` uTbl_shrStepTransTool;

    showWindow;

    setFocus uTbl_shrStepTransTool;
}

///////////////////////////////////////
// 移動タブを作成
///////////////////////////////////////
global proc shr_stepTransTool_createMoveTab()
{
    // 数値ボタン色
    float $botton_col = 0.365;

    columnLayout -adj on -p uTbl_shrStepTransTool uCl_move_shrStepTransTool;

        //座標軸アイコンボタン
        string $formLayout_name = `formLayout -width 145 -height 135`;

            $picture = `picture -image "shr_stepTransTool/bg_trans.png" -width 135 -height 135`;

            string $x_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/x_plus.png"
                -hi "shr_stepTransTool/x_plus_hover.png"
                -command "shr_stepTransTool_translate `optionVar -q shrStepTrans_selectedButtonValue` 0.0 0.0 1"
                `;

            string $x_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/x_minus.png"
                -hi "shr_stepTransTool/x_minus_hover.png"
                -command "shr_stepTransTool_translate `optionVar -q shrStepTrans_selectedButtonValue` 0.0 0.0 -1"
                `;

            string $y_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/y_plus.png"
                -hi "shr_stepTransTool/y_plus_hover.png"
                -command "shr_stepTransTool_translate 0.0 `optionVar -q shrStepTrans_selectedButtonValue` 0.0 1"
                `;

            string $y_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/y_minus.png"
                -hi "shr_stepTransTool/y_minus_hover.png"
                -command "shr_stepTransTool_translate 0.0 `optionVar -q shrStepTrans_selectedButtonValue` 0.0 -1"
                `;

            string $z_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/z_plus.png"
                -hi "shr_stepTransTool/z_plus_hover.png"
                -command "shr_stepTransTool_translate 0.0 0.0 `optionVar -q shrStepTrans_selectedButtonValue` 1"
                `;

            string $z_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/z_minus.png"
                -hi "shr_stepTransTool/z_minus_hover.png"
                -command "shr_stepTransTool_translate 0.0 0.0 `optionVar -q shrStepTrans_selectedButtonValue` -1"
                `;

            //各座標軸アイコン配置
            formLayout -edit
                -attachForm $picture "top" 0
                -attachForm $picture "left" 5

                -attachForm $x_plus "top" 69
                -attachForm $x_plus "left" 75

                -attachForm $x_minus "top" 47
                -attachForm $x_minus "left" 26

                -attachForm $y_plus "top" 6
                -attachForm $y_plus "left" 67

                -attachForm $y_minus "top" 74
                -attachForm $y_minus "left" 67

                -attachForm $z_plus "top" 69
                -attachForm $z_plus "left" 22

                -attachForm $z_minus "top" 47
                -attachForm $z_minus "left" 78

                $formLayout_name;

            setParent ..;

        separator -h 2 -st none;

        // 移動値ボタン
        rowColumnLayout
            -numberOfColumns 4
            -rowSpacing 1 2
            -columnSpacing 2 2
            -columnSpacing 3 2
            -columnSpacing 4 2
            -columnWidth 1 34
            -columnWidth 2 34
            -columnWidth 3 34
            -columnWidth 4 34;

            iconTextRadioCollection;

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_a"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_a_shrStepTransTool"
                uBtn_a_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_a `shr_stepTransTool_changeButtonValue uBtn_a_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_b"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_b_shrStepTransTool"
                uBtn_b_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_b `shr_stepTransTool_changeButtonValue uBtn_b_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_c"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_c_shrStepTransTool"
                uBtn_c_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_c `shr_stepTransTool_changeButtonValue uBtn_c_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_d"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_d_shrStepTransTool"
                uBtn_d_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_d `shr_stepTransTool_changeButtonValue uBtn_d_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_e"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_e_shrStepTransTool"
                uBtn_e_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_e `shr_stepTransTool_changeButtonValue uBtn_e_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_f"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_f_shrStepTransTool"
                uBtn_f_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_f `shr_stepTransTool_changeButtonValue uBtn_f_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_g"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_g_shrStepTransTool"
                uBtn_g_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_g `shr_stepTransTool_changeButtonValue uBtn_g_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_h"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_h_shrStepTransTool"
                uBtn_h_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "移動値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_h `shr_stepTransTool_changeButtonValue uBtn_h_shrStepTransTool`";

            iconTextRadioButton -e -sl `optionVar -q "shrStepTrans_selectedButtonName"`;

            setParent ..;

        separator -h 8 -st "in";

        rowColumnLayout -nc 2 -rs 1 2 -rs 2 2;
            text -l "    単位:";
            optionMenu
                -annotation "数値単位を選択"
                -w 100
                -changeCommand "optionVar -iv shrStepTrans_selectedUnit `optionMenu -q -select uOm_unit_shrStepTransTool`; setFocus uTbl_shrStepTransTool"
                uOm_unit_shrStepTransTool;

    			menuItem -label "Maya Unit";
    			menuItem -label "m";
    			menuItem -label "cm";
    			menuItem -label "mm";

    			optionMenu -e -select `optionVar -q "shrStepTrans_selectedUnit"` uOm_unit_shrStepTransTool;

            text -l " 座標系:";
            optionMenu
                -annotation "座標系を選択"
                -changeCommand "optionVar -iv shrStepTrans_selectedAxis `optionMenu -q -select uOm_axis_shrStepTransTool`; setFocus uTbl_shrStepTransTool; shr_stepTransTool_changeMoveAxis;"
                uOm_axis_shrStepTransTool;

        		menuItem -label "オブジェクト";
        		menuItem -label "ペアレント";
        		menuItem -label "ワールド";

                optionMenu -e -select `optionVar -q "shrStepTrans_selectedAxis"` uOm_axis_shrStepTransTool;

            text -l "";

            checkBox
                -l "UVの保持"
                -v `optionVar -q "shrStepTrans_preserveUVs"`
                -onCommand "optionVar -iv shrStepTrans_preserveUVs true"
                -offCommand "optionVar -iv shrStepTrans_preserveUVs false"
                uCb_preserveUVs_shrStepTrans;
}

///////////////////////////////////////
// 回転タブを作成
///////////////////////////////////////
global proc shr_stepTransTool_createRotateTab()
{
    // 数値ボタン色
    float $botton_col = 0.365;

    columnLayout -adj on -p uTbl_shrStepTransTool uCl_rotate_shrStepTransTool;

        //座標軸アイコンボタン
        string $formLayout_name = `formLayout -width 135 -height 135`;

            $picture = `picture -image "shr_stepTransTool/bg_rot.png" -width 135 -height 135`;

            string $x_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_x_plus.png"
                -hi "shr_stepTransTool/rot_x_plus_hover.png"
                -command "shr_stepTransTool_rotate `optionVar -q shrStepTrans_selectedButtonValue_rot` 0.0 0.0 1"
                `;

            string $x_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_x_minus.png"
                -hi "shr_stepTransTool/rot_x_minus_hover.png"
                -command "shr_stepTransTool_rotate `optionVar -q shrStepTrans_selectedButtonValue_rot` 0.0 0.0 -1"
                `;

            string $y_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_y_plus.png"
                -hi "shr_stepTransTool/rot_y_plus_hover.png"
                -command "shr_stepTransTool_rotate 0.0 `optionVar -q shrStepTrans_selectedButtonValue_rot` 0.0 1"
                `;

            string $y_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_y_minus.png"
                -hi "shr_stepTransTool/rot_y_minus_hover.png"
                -command "shr_stepTransTool_rotate 0.0 `optionVar -q shrStepTrans_selectedButtonValue_rot` 0.0 -1"
                `;

            string $z_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_z_plus.png"
                -hi "shr_stepTransTool/rot_z_plus_hover.png"
                -command "shr_stepTransTool_rotate 0.0 0.0 `optionVar -q shrStepTrans_selectedButtonValue_rot` 1"
                `;

            string $z_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/rot_z_minus.png"
                -hi "shr_stepTransTool/rot_z_minus_hover.png"
                -command "shr_stepTransTool_rotate 0.0 0.0 `optionVar -q shrStepTrans_selectedButtonValue_rot` -1"
                `;

            //各座標軸アイコン配置
            formLayout -edit
                -attachForm $picture "top" 0
                -attachForm $picture "left" 5

                -attachForm $x_plus "top" 56
                -attachForm $x_plus "left" 22

                -attachForm $x_minus "top" 22
                -attachForm $x_minus "left" 37

                -attachForm $y_plus "top" 102
                -attachForm $y_plus "left" 72

                -attachForm $y_minus "top" 102
                -attachForm $y_minus "left" 26

                -attachForm $z_plus "top" 22
                -attachForm $z_plus "left" 72

                -attachForm $z_minus "top" 56
                -attachForm $z_minus "left" 105

                $formLayout_name;

            setParent ..;

        separator -h 2 -st none;

        // 回転値ボタン
        rowColumnLayout
            -numberOfColumns 4
            -rowSpacing 1 2
            -columnSpacing 2 2
            -columnSpacing 3 2
            -columnSpacing 4 2
            -columnWidth 1 34
            -columnWidth 2 34
            -columnWidth 3 34
            -columnWidth 4 34;

            iconTextRadioCollection;

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_a"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_a_shrStepTransTool"
                uBtn_rot_a_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_a `shr_stepTransTool_changeButtonValue uBtn_rot_a_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_b"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_b_shrStepTransTool"
                uBtn_rot_b_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_b `shr_stepTransTool_changeButtonValue uBtn_rot_b_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_c"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_c_shrStepTransTool"
                uBtn_rot_c_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_c `shr_stepTransTool_changeButtonValue uBtn_rot_c_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_d"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_d_shrStepTransTool"
                uBtn_rot_d_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_d `shr_stepTransTool_changeButtonValue uBtn_rot_d_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_e"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_e_shrStepTransTool"
                uBtn_rot_e_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_e `shr_stepTransTool_changeButtonValue uBtn_rot_e_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_f"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_f_shrStepTransTool"
                uBtn_rot_f_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_f `shr_stepTransTool_changeButtonValue uBtn_rot_f_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_g"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_g_shrStepTransTool"
                uBtn_rot_g_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_g `shr_stepTransTool_changeButtonValue uBtn_rot_g_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_rot_h"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_rot_h_shrStepTransTool"
                uBtn_rot_h_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "回転値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_rot_h `shr_stepTransTool_changeButtonValue uBtn_rot_h_shrStepTransTool`";

            iconTextRadioButton -e -sl `optionVar -q "shrStepTrans_selectedButtonName_rot"`;

            setParent ..;

        separator -h 8 -st "in";

        rowColumnLayout -nc 2 -rs 1 2 -rs 2 2;
            text -l "    中心:";
            optionMenu
                -w 100
                -annotation "回転の中心を選択"
                -changeCommand "optionVar -iv shrStepTrans_selectedRotCenter `optionMenu -q -select uOm_rot_center_shrStepTransTool`; setFocus uTbl_shrStepTransTool; "
                uOm_rot_center_shrStepTransTool
                ;

    			//menuItem -label "デフォルト";
    			menuItem -label "オブジェクト";
    			menuItem -label "選択の中心";

    			optionMenu -e -select `optionVar -q "shrStepTrans_selectedRotCenter"` uOm_rot_center_shrStepTransTool;

            text -l " 座標系:";
            optionMenu
                -annotation "座標系を選択"
                -changeCommand "optionVar -iv shrStepTrans_selectedAxis_rot `optionMenu -q -select uOm_rot_axis_shrStepTransTool`; setFocus uTbl_shrStepTransTool; shr_stepTransTool_changeRotAxis"
                uOm_rot_axis_shrStepTransTool;

        		menuItem -label "オブジェクト";
        		menuItem -label "ワールド";

                optionMenu -e -select `optionVar -q "shrStepTrans_selectedAxis_rot"` uOm_rot_axis_shrStepTransTool;

            text -l "";

            checkBox
                -l "UVの保持"
                -v `optionVar -q "shrStepTrans_preserveUVs_rot"`
                -onCommand "optionVar -iv shrStepTrans_preserveUVs_rot true"
                -offCommand "optionVar -iv shrStepTrans_preserveUVs_rot false"
                uCb_rot_preserveUVs_shrStepTrans;
}

///////////////////////////////////////
// スケールタブを作成
///////////////////////////////////////
global proc shr_stepTransTool_createScaleTab()
{
    // 数値ボタン色
    float $botton_col = 0.365;

    columnLayout -adj on -p uTbl_shrStepTransTool uCl_scale_shrStepTransTool;

        //座標軸アイコンボタン
        string $formLayout_name = `formLayout -width 135 -height 135`;

            $picture = `picture -image "shr_stepTransTool/bg_scale.png" -width 135 -height 135`;

            string $x_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_x_plus.png"
                -hi "shr_stepTransTool/scale_x_plus_hover.png"
                -command "shr_stepTransTool_scale `optionVar -q shrStepTrans_selectedButtonValue_scale` 1 1"
                `;

            string $x_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_x_minus.png"
                -hi "shr_stepTransTool/scale_x_minus_hover.png"
                -command "shr_stepTransTool_scale (1.0 / `optionVar -q shrStepTrans_selectedButtonValue_scale`) 1 1"
                `;

            string $y_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_y_plus.png"
                -hi "shr_stepTransTool/scale_y_plus_hover.png"
                -command "shr_stepTransTool_scale 1 `optionVar -q shrStepTrans_selectedButtonValue_scale` 1"
                `;

            string $y_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_y_minus.png"
                -hi "shr_stepTransTool/scale_y_minus_hover.png"
                -command "shr_stepTransTool_scale 1 (1.0 / `optionVar -q shrStepTrans_selectedButtonValue_scale`) 1"
                `;

            string $z_plus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_z_plus.png"
                -hi "shr_stepTransTool/scale_z_plus_hover.png"
                -command "shr_stepTransTool_scale 1 1 `optionVar -q shrStepTrans_selectedButtonValue_scale`"
                `;

            string $z_minus = `iconTextButton
                -style "iconOnly"
                -image1 "shr_stepTransTool/scale_z_minus.png"
                -hi "shr_stepTransTool/scale_z_minus_hover.png"
                -command "shr_stepTransTool_scale 1 1 (1.0 / `optionVar -q shrStepTrans_selectedButtonValue_scale`)"
                `;

            //各座標軸アイコン配置
            formLayout -edit
                -attachForm $picture "top" 0
                -attachForm $picture "left" 5

                -attachForm $x_plus "top" 69
                -attachForm $x_plus "left" 76

                -attachForm $x_minus "top" 42
                -attachForm $x_minus "left" 26

                -attachForm $y_plus "top" 9
                -attachForm $y_plus "left" 64

                -attachForm $y_minus "top" 74
                -attachForm $y_minus "left" 65

                -attachForm $z_plus "top" 69
                -attachForm $z_plus "left" 23

                -attachForm $z_minus "top" 42
                -attachForm $z_minus "left" 77

                $formLayout_name;

            setParent ..;

        separator -h 2 -st none;

        // スケール値ボタン
        rowColumnLayout
            -numberOfColumns 4
            -rowSpacing 1 2
            -columnSpacing 2 2
            -columnSpacing 3 2
            -columnSpacing 4 2
            -columnWidth 1 34
            -columnWidth 2 34
            -columnWidth 3 34
            -columnWidth 4 34;

            iconTextRadioCollection;

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_a"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_a_shrStepTransTool"
                uBtn_scale_a_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_a `shr_stepTransTool_changeButtonValue uBtn_scale_a_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_b"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_b_shrStepTransTool"
                uBtn_scale_b_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_b `shr_stepTransTool_changeButtonValue uBtn_scale_b_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_c"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_c_shrStepTransTool"
                uBtn_scale_c_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_c `shr_stepTransTool_changeButtonValue uBtn_scale_c_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_d"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_d_shrStepTransTool"
                uBtn_scale_d_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_d `shr_stepTransTool_changeButtonValue uBtn_scale_d_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_e"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_e_shrStepTransTool"
                uBtn_scale_e_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_e `shr_stepTransTool_changeButtonValue uBtn_scale_e_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_f"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_f_shrStepTransTool"
                uBtn_scale_f_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_f `shr_stepTransTool_changeButtonValue uBtn_scale_f_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_g"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_g_shrStepTransTool"
                uBtn_scale_g_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_g `shr_stepTransTool_changeButtonValue uBtn_scale_g_shrStepTransTool`";

            iconTextRadioButton
                -label `optionVar -q "shrStepTrans_buttonValue_scale_h"`
                -height 18
                -style textOnly
                -bgc $botton_col $botton_col $botton_col
                -onCommand "shr_stepTransTool_radioButtonSelected uBtn_scale_h_shrStepTransTool"
                uBtn_scale_h_shrStepTransTool;
                popupMenu;
                    menuItem
                    -label "スケール値の変更"
                    -command "optionVar -fv shrStepTrans_buttonValue_scale_h `shr_stepTransTool_changeButtonValue uBtn_scale_h_shrStepTransTool`";

            iconTextRadioButton -e -sl `optionVar -q "shrStepTrans_selectedButtonName_scale"`;

            setParent ..;

        separator -h 8 -st "in";

        rowColumnLayout -nc 2 -rs 1 2 -rs 2 2;
            text -l "    中心:";
            optionMenu
                -w 100
                -annotation "スケールの中心を選択"
                -changeCommand "optionVar -iv shrStepTrans_selectedScaleCenter `optionMenu -q -select uOm_scale_center_shrStepTransTool`; setFocus uTbl_shrStepTransTool; "
                uOm_scale_center_shrStepTransTool;

            menuItem -label "オブジェクト";
            menuItem -label "選択の中心";

    			optionMenu -e -select `optionVar -q "shrStepTrans_selectedScaleCenter"` uOm_scale_center_shrStepTransTool;

            text -l " 座標系:";
            optionMenu
                -annotation "座標系を選択"
                -changeCommand "optionVar -iv shrStepTrans_selectedAxis_scale `optionMenu -q -select uOm_scale_axis_shrStepTransTool`; setFocus uTbl_shrStepTransTool; shr_stepTransTool_changeScaleAxis"
                uOm_scale_axis_shrStepTransTool;

        		menuItem -label "オブジェクト";
        		menuItem -label "ペアレント";
        		menuItem -label "ワールド";

                optionMenu -e -select `optionVar -q "shrStepTrans_selectedAxis_scale"` uOm_scale_axis_shrStepTransTool;

            text -l "";

            checkBox
                -l "UVの保持"
                -v `optionVar -q "shrStepTrans_preserveUVs_scale"`
                -onCommand "optionVar -iv shrStepTrans_preserveUVs_scale true"
                -offCommand "optionVar -iv shrStepTrans_preserveUVs_scale false"
                uCb_scale_preserveUVs_shrStepTrans;

            text -l "";

            checkBox
                -l "均等スケール"
                -v `optionVar -q "shrStepTrans_uniformScale_scale"`
                -onCommand "optionVar -iv shrStepTrans_uniformScale_scale true"
                -offCommand "optionVar -iv shrStepTrans_uniformScale_scale false"
                uCb_scale_uniformScale_shrStepTrans;
}

///////////////////////////////////////
// 数値ボタンを選択コールバック
///////////////////////////////////////
global proc shr_stepTransTool_radioButtonSelected(string $selectedButtonName)
{
    // オプション名
    string $option_buttonName = "shrStepTrans_selectedButtonName";
    string $option_buttonValue = "shrStepTrans_selectedButtonValue";

    string $suffix;

    // 回転値ボタンの場合
    if(startsWith($selectedButtonName, "uBtn_rot_"))
    {
        $suffix = "_rot";
    }
    // スケールボタンの場合
    else if(startsWith($selectedButtonName, "uBtn_scale_"))
    {
        $suffix = "_scale";
    }

    optionVar -sv ($option_buttonName + $suffix) $selectedButtonName;

    float $value = float(`iconTextRadioButton -q -label $selectedButtonName`);

    optionVar -fv ($option_buttonValue + $suffix) $value;
}

///////////////////////////////////////
// 移動アイコンボタン コールバック
///////////////////////////////////////
global proc shr_stepTransTool_translate(float $x, float $y, float $z, int $plusMinusSign)
{
    string $axis = `optionMenu -q -value uOm_axis_shrStepTransTool`;

    string $unit = `optionMenu -q -value uOm_unit_shrStepTransTool`;

    float $unitsPerMeter = `optionVar -q "shrStepTrans_unitsPerMeter"`;

    float $multiValue = 1;

    switch($unit)
    {
        case "m":
            $multiValue = $unitsPerMeter;
            break;
        case "cm":
            $multiValue = $unitsPerMeter / 100;
            break;
        case "mm":
            $multiValue = $unitsPerMeter / 1000;
            break;
    }

    $x = $x * $plusMinusSign * $multiValue;
    $y = $y * $plusMinusSign * $multiValue;
    $z = $z * $plusMinusSign * $multiValue;

    // UVの保持がオン
    if (`checkBox -q -v uCb_preserveUVs_shrStepTrans`)
    {
		switch($axis)
		{
    		case "オブジェクト":
    			move -r -os -wd -puv $x $y $z;
    			break;
    		case "ペアレント":
    			move -r -ls -wd -puv $x $y $z;
    			break;
    		case "ワールド":
    			move -r -puv $x $y $z;
    			break;
		}
    }
    // UVの保持がオフ
    else
    {
		switch($axis)
		{
    		case "オブジェクト":
    			move -r -os -wd $x $y $z;
    			break;
    		case "ペアレント":
    			move -r -ls -wd $x $y $z;
    			break;
    		case "ワールド":
    			move -r $x $y $z;
    			break;
		}
     }

    setFocus uTbl_shrStepTransTool;
}

///////////////////////////////////////
// 回転アイコンボタン コールバック
///////////////////////////////////////
global proc shr_stepTransTool_rotate(float $x, float $y, float $z, int $plusMinusSign)
{
    string $axis = `optionMenu -q -value uOm_rot_axis_shrStepTransTool`;

    string $center = `optionMenu -q -value uOm_rot_center_shrStepTransTool`;

    $x = $x * $plusMinusSign;
    $y = $y * $plusMinusSign;
    $z = $z * $plusMinusSign;

    string $axisOption;
    string $centerOption;

    switch($axis)
    {
        case "オブジェクト":
            $axisOption = "-os ";

            break;

        case "ワールド":
            $axisOption = "-ws ";

            break;
    }

    switch($center)
    {
        case "オブジェクト":
            $centerOption = "-ocp ";

            break;

        case "選択の中心":
            $centerOption = "-cp ";

            break;
    }

    string $rotCmd = "rotate -r -fo " + $centerOption + $axisOption + $x + " " + $y + " " + $z;

    if(`checkBox -q -v uCb_rot_preserveUVs_shrStepTrans`)
    {
        $rotCmd = $rotCmd + " -puv";
    }

    // 回転実行
    eval $rotCmd;

    setFocus uTbl_shrStepTransTool;
}

///////////////////////////////////////
// スケールアイコンボタン コールバック
///////////////////////////////////////
global proc shr_stepTransTool_scale(float $x, float $y, float $z)
{
    string $axis = `optionMenu -q -value uOm_scale_axis_shrStepTransTool`;

    string $center = `optionMenu -q -value uOm_scale_center_shrStepTransTool`;

    string $axisOption;
    string $centerOption;

    switch($axis)
    {
        case "オブジェクト":
            $axisOption = "-os ";

            break;

        case "ペアレント":
            $axisOption = "-ls ";

            break;

        case "ワールド":
            $axisOption = "-ws ";

            break;
    }

    switch($center)
    {
        case "オブジェクト":
            $centerOption = "";

            break;

        case "選択の中心":
            $centerOption = "-cp ";

            break;
    }

    // 均等スケールの場合
    if(`checkBox -q -v uCb_scale_uniformScale_shrStepTrans`)
    {
        if($x != 1)
        {
            $y = $x;
            $z = $x;
        }
        else if($y != 1)
        {
            $x = $y;
            $z = $y;
        }
        else
        {
            $x = $z;
            $y = $z;
        }
    }


    string $scaleCmd = "scale -r " + $centerOption + $axisOption + $x + " " + $y + " " + $z;

    if(`checkBox -q -v uCb_scale_preserveUVs_shrStepTrans`)
    {
        $scaleCmd = $scaleCmd + " -puv";
    }

    // スケール実行
    eval $scaleCmd;

    setFocus uTbl_shrStepTransTool;
}

///////////////////////////////////////
// 数値ボタン 右クリックコールバック
///////////////////////////////////////
global proc float shr_stepTransTool_changeButtonValue(string $selectedButtonName)
{
    float $selectedButtonValue =`iconTextRadioButton -q -label $selectedButtonName`;

    string $str;

    // 回転値ボタンの場合
    if(startsWith($selectedButtonName, "uBtn_rot_"))
    {
        $str = "回転値";
    }
    // スケールボタンの場合
    else if(startsWith($selectedButtonName, "uBtn_scale_"))
    {
        $str = "スケール値";
    }
    else
    {
        $str = "移動値";
    }

    //移動値入力ダイアログ
	string $resault =
    	`promptDialog
        	-title ($str + "の変更")
        	-message ($str + "を入力してください。")
        	-text $selectedButtonValue
        	-button "決定"
        	-button "キャンセル"
        	-defaultButton "決定"
        	-cancelButton "キャンセル"
        	-dismissString "キャンセル"`;

    //入力確定したら
    if($resault == "決定")
    {
        $selectedButtonValue = `promptDialog -q`;

        iconTextRadioButton -e -label $selectedButtonValue $selectedButtonName;

        // オプション名
        string $option_buttonName = "shrStepTrans_selectedButtonName";
        string $option_buttonValue = "shrStepTrans_selectedButtonValue";

        string $suffix;

        // 回転値ボタンの場合
        if(startsWith($selectedButtonName, "uBtn_rot_"))
        {
            $suffix = "_rot";
        }
        // スケールボタンの場合
        else if(startsWith($selectedButtonName, "uBtn_scale_"))
        {
            $suffix = "_scale";
        }

        //数値変更したボタンが選択中のボタンなら、値変更
        if ($selectedButtonName == `optionVar -q ($option_buttonName + $suffix)`)
        {
            optionVar -fv ($option_buttonValue + $suffix) $selectedButtonValue;
        }
    }

    return $selectedButtonValue;
}

///////////////////////////////////////
// 移動ツール 軸マニピュレータ変更
///////////////////////////////////////
global proc shr_stepTransTool_changeMoveAxis()
{
    if(`optionVar -q shrStepTrans_syncTransformManip` == false)
    {
        return;
    }

    string $moveAxis = `optionMenu -q -value uOm_axis_shrStepTransTool`;

    //移動ツールの座標軸設定を変更
    switch($moveAxis)
    {
        case "オブジェクト":
            manipMoveContext -e -mode 0 Move;
            break;
        case "ペアレント":
            manipMoveContext -e -mode 1 Move;
            break;
        case "ワールド":
            manipMoveContext -e -mode 2 Move;
            break;
    }
}

///////////////////////////////////////
// 回転ツール 軸マニピュレータ変更
///////////////////////////////////////
global proc shr_stepTransTool_changeRotAxis()
{
    if(`optionVar -q shrStepTrans_syncTransformManip` == false)
    {
        return;
    }

    string $rotAxis = `optionMenu -q -value uOm_rot_axis_shrStepTransTool`;

    //回転ツールの回転モード設定を変更
    switch($rotAxis)
    {
        case "オブジェクト":
            manipRotateContext -e -mode 0 Rotate;
            break;
        case "ワールド":
            manipRotateContext -e -mode 1 Rotate;
            break;
    }
}

///////////////////////////////////////
// 回転ツール 回転ピボット変更
///////////////////////////////////////
global proc shr_stepTransTool_changeRotPivot()
{
    string $rotPivot = `optionMenu -q -value uOm_rot_center_shrStepTransTool`;

    //回転ツールの回転ピボット設定を変更
    switch($rotPivot)
    {
        case "オブジェクト":
            manipRotateContext -e -useManipPivot 0 -useObjectPivot 1 -useCenterPivot 0 Rotate;
            break;
        case "選択の中心":
            manipRotateContext -e -useManipPivot 0 -useObjectPivot 0 -useCenterPivot 1 Rotate;
            break;
    }
}

///////////////////////////////////////
// スケールツール 軸マニピュレータ変更
///////////////////////////////////////
global proc shr_stepTransTool_changeScaleAxis()
{
    if(`optionVar -q shrStepTrans_syncTransformManip` == false)
    {
        return;
    }

    string $scaleAxis = `optionMenu -q -value uOm_scale_axis_shrStepTransTool`;

    //スケールツールの座標軸設定を変更
    switch($scaleAxis)
    {
        case "オブジェクト":
            manipScaleContext -e -mode 0 Scale;
            break;
        case "ペアレント":
            manipScaleContext -e -mode 1 Scale;
            break;
        case "ワールド":
            manipScaleContext -e -mode 2 Scale;
            break;
    }
}

///////////////////////////////////////
// スケールツール スケールピボット変更
///////////////////////////////////////
global proc shr_stepTransTool_changeScalePivot()
{
    string $scalePivot = `optionMenu -q -value uOm_scale_center_shrStepTransTool`;

    //スケールツールのスケールピボット設定を変更
    switch($scalePivot)
    {
        case "オブジェクト":
            manipScaleContext -e -useManipPivot 0 -useObjectPivot 1 Scale;
            break;
        case "選択の中心":
            manipScaleContext -e -useManipPivot 1 -useObjectPivot 0 Scale;
            break;
    }
}

//===============================================
// 設定ダイアログ作成
//===============================================
global proc shr_stepTransTool_optionDialog()
{
    // 1m あたりのMayaユニット数
    float $unitsPerMeter = `optionVar -q "shrStepTrans_unitsPerMeter"`;

    // ダイアログのformLayout を取得
    string $form = `setParent -q`;

    formLayout -e -width 270 $form;

    string $rcl = `rowColumnLayout -nc 3 -cw 2 75 -rs 2 8`;

    string $t1 = `text -l "1 m = "`;
    string $fField = `floatField -min 0.0001 -v $unitsPerMeter -pre 1`;
    string $t2 = `text -l " units"`;

    setParent ..;

    string $b = `button -l "OK" -c "layoutDialog -dismiss \"ok\""`;

    formLayout -e
        -attachForm    $rcl   "top"    20
        -attachForm    $rcl   "left"   64
        -attachNone    $rcl   "bottom"
        -attachForm    $rcl   "right"  1

        -attachNone    $b   "top"
        -attachForm    $b   "left"   1
        -attachForm    $b   "bottom" 1
        -attachForm    $b   "right"  1
    $form;

    string $cmd = "optionVar -fv shrStepTrans_unitsPerMeter `floatField -q -v " + $fField + "`";

    floatField -e -cc $cmd $fField;
}
