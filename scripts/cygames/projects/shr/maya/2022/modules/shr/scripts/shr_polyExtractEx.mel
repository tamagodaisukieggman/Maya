//===============================================
//
// ポリゴンの抽出Ex
//
// Fujita Yukihiro
//
//===============================================

global proc shr_polyExtractEx()
{
    // TA ツールログ送信
    shr_toolLog("Extract Ex", "1.0.0", "launched.");

    // 選択ポリゴン取得
    string $selPolygon[] = `filterExpand -sm 34`;

    // ポリゴンが選択されていなかったら終了
    if (`size($selPolygon)` == 0)
    {
        print "抽出するポリゴンが選択されていません。";
        return;
    }

    // ポリゴン抽出オプションを選択
    string $extractPolyOption = (`layoutDialog -title "Extract Polygon Ex" -ui "shr_polyExtractEx_Prompt"`);

    shr_polyExtractEx_exec($extractPolyOption);
}

global proc shr_polyExtractEx_exec(string $extractPolyOption)
{
    if (($extractPolyOption == "キャンセル") || ($extractPolyOption == "却下") || ($extractPolyOption == "dismiss"))
    {
        return;
    }

    string $cursel[];
    string $sel[] = `ls -sl`;
    int $index=0;
    int $size = `size ($sel)`;

    string $newObj[];

    while($index < $size)
    {
    	$index = `polyNextSelectionBatch $sel $cursel $index`;
    	select -r $cursel;
    	string $oneObjFacets[] = `ls -sl`;
    	string $oneObjShape[] = `ls -sl -o`;
    	string $newName[] = `duplicate -st $oneObjShape[0]`;
        $newObj[size($newObj)] = $newName[0];
    	int $i;
    	string $subed[];

        for($i = 0; $i < `size ($cursel)`; $i++)
        {
        	$subed[$i] = `substitute ".*\\."  $cursel[$i] ($newName[0]+".")`;
        }

		select -r ($newName[0]+".f[*]");
		select -d $subed;
		delete;
	}

    // 抽出（削除）の場合
    if ($extractPolyOption == "削除")
    {
        delete $sel;
    }

    // 選択モードをオブジェクトに
    changeSelectMode -object;

    // 抽出したオブジェクトを選択
    select -r $newObj;

    // 中央にピボットポイントを移動するなら
    if (`optionVar -q shr_polyExtractEx_cnterPvt` == 1)
    {
        // 中央にピボットポイントを移動
        CenterPivot;

        // ピボットをベイク
        performBakeCustomToolPivot(0);
    }

    // コンポーネントのトランスフォームを実行
    if (`optionVar -q shr_polyExtractEx_doTransform` == 1)
    {
        // 生成されたすべてのpolyExtrude ノード
        string $newPolyExtrudeNodeAll[];

        string $elm;

        for($elm in $newObj)
        {
            // 現在のすべてのポリゴンを取得
            string $currentAllFaces[] = `ls -flatten ($elm + ".f[*]")`;

            // ポリゴンを押し出し
            string $newPolyExtrudeNode[] = `polyExtrudeFacet -ch 1 -kft 1 -d 1 -twist 0 -taper 1 -off 0 -smoothingAngle 30 $currentAllFaces`;

            $newPolyExtrudeNode[0] = `rename $newPolyExtrudeNode[0] "polyExtractEx"`;

            // polyExtrudeNode リストに追加
            $newPolyExtrudeNodeAll[`size($newPolyExtrudeNodeAll)`] = $newPolyExtrudeNode[0];

            // 押し出されたサイドのポリゴンを取得
            string $newFaces[] = stringArrayRemove($currentAllFaces, `ls -flatten ($elm + ".f[*]")`);

            // サイドのポリゴンを削除
            delete $newFaces;
        }

        // 抽出したオブジェクトを選択
        select -r $newObj;

        // ポリゴン選択
        ConvertSelectionToFaces;

        setSelectMode components Components;

        // polyExtrude ノード選択
        select -addFirst $newPolyExtrudeNodeAll;

        // ツールのマニピュレータ表示
        setToolTo "ShowManips";
    }
}


global proc shr_polyExtractEx_Prompt()
{
    string $form = `setParent -q`;

    formLayout -e -width 280 -h 130 $form;

    string $t = "";

	string $b1 = "";
	string $b2 = "";
	string $b3 = "";

	$t = `text -l "ポリゴン抽出方法を選択して下さい。"`;
	$b1 = `button -l "削除" -c "layoutDialog -dismiss \"削除\"" -ann "元のポリゴンを削除して抽出します"`;
	$b2 = `button -l "保持" -c "layoutDialog -dismiss \"保持\"" -ann "元のポリゴンを削除せずに抽出します"`;
	$b3 = `button -l "キャンセル" -c "layoutDialog -dismiss \"キャンセル\""`;

    int $cb1_value = `optionVar -q "shr_polyExtractEx_cnterPvt"`;
    int $cb2_value = `optionVar -q "shr_polyExtractEx_doTransform"`;

    string $cb1 = `checkBox -onc "optionVar -iv shr_polyExtractEx_cnterPvt 1" -ofc "optionVar -iv shr_polyExtractEx_cnterPvt 0" -v $cb1_value -label "中央にピボットポイントを移動" -ann "抽出したオブジェクトのピボットポイントを中央に移動します"`;
    string $cb2 = `checkBox -onc "optionVar -iv shr_polyExtractEx_doTransform  1" -ofc "optionVar -iv shr_polyExtractEx_doTransform 0" -v $cb2_value -label "コンポーネントのトランスフォームを実行" -ann "抽出後にコンポーネントのトランスフォームを実行します"`;

    formLayout -edit
        -attachForm            $t   "top"    16
        -attachForm            $t   "left"   5
        -attachNone            $t   "bottom"
        -attachForm            $t   "right"  5

        -attachControl         $b1  "top"    10 $t
        -attachForm            $b1  "left"   10
        -attachNone            $b1  "bottom"
        -attachPosition        $b1  "right"  5 33

        -attachControl         $b2  "top"    10 $t
        -attachPosition        $b2  "left"   5 33
        -attachNone            $b2  "bottom"
        -attachPosition        $b2  "right"  5 66

        -attachControl         $b3  "top"    10 $t
        -attachPosition        $b3  "left"   5 66
        -attachNone            $b3  "bottom"
        -attachForm            $b3  "right"  10

        -attachControl         $cb1 "top"    16 $b1
        -attachForm            $cb1 "left"   80
        -attachNone            $cb1 "bottom"
        -attachNone            $cb1 "right"

        -attachControl         $cb2 "top"    4 $cb1
        -attachForm            $cb2 "left"   80
        -attachNone            $cb2 "bottom"
        -attachNone            $cb2 "right"
    $form;

    setFocus $b1;
}
