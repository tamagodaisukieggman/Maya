
//-------------------------------------------------------------------------------------------//

global proc yk_PivotEditor_Cmds(int $flag , int $minus)
{

string $sele[] = `ls -sl -fl -type transform`;
if((`size $sele`) == 0){return;}

string $x , $model_1[] , $model_2[] , $un_parent[];
float $xyz[];

for($x in $sele){
    $model_1 = `duplicate -rr $x`; 
    $un_parent[0] = $model_1[0];  //----複製したモデル
    string $child[] = `listRelatives -p -f $un_parent[0]`;
    if(`size $child` > 0){  //----親がある場合はペアレントしてそれを$un_parent[0]に入れる
        $un_parent = `parent -w $model_1[0]`;
    }
    if(`checkBox -q -v yk_FGH4` == 1){  //---ONの時はセンターのピボット座標を取得できるように
        select $un_parent[0];
        CenterPivot $un_parent[0];
    }
    $xyz = `xform -q -t -ws ($un_parent[0] + ".rotatePivot") `;  //----ピボット位置を取得しておく
    makeIdentity -a 1 -t 1 -r 1 -s 1 -n 0 -pn 1 $un_parent[0];  //----フリーズしたBBを取得する
    $model_2 = `lattice -divisions 2 2 2 -objectCentered 1 $un_parent[0]`;
    float $bb_xyz[] = `xform -q -ws -bb $model_2[1]`;
    
    float $result[2] = {$bb_xyz[3] , $bb_xyz[4] , $bb_xyz[5]};  //----BBの値を入れる
    if($minus == -1){  //----マイナスフラグ
        $result[0] = $bb_xyz[0];  //----マイナスは最小値を入れる
        $result[1] = $bb_xyz[1];
        $result[2] = $bb_xyz[2];
    }
    
    if($flag == 1){  //----x
	    move $result[0] ($xyz[1]) ($xyz[2]) ($x + ".scalePivot") ($x + ".rotatePivot"); 
    }
    else if($flag == 2){  //----y
	    move ($xyz[0]) $result[1] ($xyz[2]) ($x + ".scalePivot") ($x + ".rotatePivot"); 
    }
    else if($flag == 3){  //----z
	    move ($xyz[0]) ($xyz[1]) $result[2] ($x + ".scalePivot") ($x + ".rotatePivot"); 
    }
    
    clear $result $bb_xyz $xyz; 
    delete $un_parent[0] $model_2;  //----複製したモデルとラティスモデル削除   
}

select $sele;  

}

//-------------------------------------------------------------------------------------------//ピボットゲット

global proc yk_PivotEditor_Get()
{

string $sele_model[] = `ls -sl -type transform`;
string $sele_vertex[] = `filterExpand -sm 31`;
if((`size $sele_model`) == 0 && (`size $sele_vertex`) == 0){return;}

float $v_x[] , $v_y[] , $v_z[];
string $x;
int $i = 0;

if((`size $sele_model`) > 0){  //----モデル選択のとき
    for($x in $sele_model){
        float $pivot[] = `xform -q -t -ws ($x + ".rotatePivot")`;
        $v_x[$i] = $pivot[0];
        $v_y[$i] = $pivot[1];
        $v_z[$i] = $pivot[2];     
        $i++;
    }
}
else if((`size $sele_vertex`) > 0){  //----頂点選択の時
    for($x in $sele_vertex){
        float $vertex[] = `xform -q -t -ws $x`;
        $v_x[$i] = $vertex[0];
        $v_y[$i] = $vertex[1];
        $v_z[$i] = $vertex[2];     
        $i++;
    }
}

$v_x = `sort $v_x`;
$v_y = `sort $v_y`;
$v_z = `sort $v_z`;
float $get_x = (($v_x[0] + $v_x[((`size $v_x`) - 1)]) / 2);
float $get_y = (($v_y[0] + $v_y[((`size $v_y`) - 1)]) / 2);
float $get_z = (($v_z[0] + $v_z[((`size $v_z`) - 1)]) / 2);

floatField -e -v $get_x yk_PivotEditor_FLOATFIELD1;
floatField -e -v $get_y yk_PivotEditor_FLOATFIELD2;
floatField -e -v $get_z yk_PivotEditor_FLOATFIELD3;
  
}

//-------------------------------------------------------------------------------------------//ピボット移動

global proc yk_PivotEditor_Move()
{

string $sele[] = `ls -sl -type transform`;
if((`size $sele`) == 0){return;}

float $xyz[];
$xyz[0] = `floatField -q -v yk_PivotEditor_FLOATFIELD1`;
$xyz[1] = `floatField -q -v yk_PivotEditor_FLOATFIELD2`;
$xyz[2] = `floatField -q -v yk_PivotEditor_FLOATFIELD3`;

string $x;
for($x in $sele){
    float $get[] = `xform -q -t -ws ($x + ".rotatePivot")`;
    move -r ($xyz[0] - $get[0]) ($xyz[1] - $get[1]) ($xyz[2] - $get[2]) $x;
}
    
}

//-------------------------------------------------------------------------------------------//ピボットコピー

global proc yk_PivotEditor_Copy()
{

string $sele[] = `ls -sl -type transform`;
if((`size $sele`) == 0){return;}

float $xyz[];
$xyz[0] = `floatField -q -v yk_PivotEditor_FLOATFIELD1`;
$xyz[1] = `floatField -q -v yk_PivotEditor_FLOATFIELD2`;
$xyz[2] = `floatField -q -v yk_PivotEditor_FLOATFIELD3`;

string $x;
for($x in $sele){
    move $xyz[0] $xyz[1] $xyz[2] ($x + ".scalePivot") ($x + ".rotatePivot");
}
    
}

//-------------------------------------------------------------------------------------------//センター

global proc yk_PivotEditor_Center()
{
    
string $select[] = `ls -sl`; 
if(`size $select` == 0){return;}  //---選択0なら終了
   
string $model_sele[] = `ls -sl -type transform`;
if((`size $model_sele`) > 0){  //----モデルが選択されていれば処理
	CenterPivot;
	return;
}

string $sele[] = `filterExpand -sm 32 -sm 34`;  //----頂点、面選択の処理
if((`size $sele`) != 1){
    confirmDialog -t "Confirm" -m "Edge、Faceの場合、複数選択はできません" -b "OK" ;
    return;
}

int $i = 0;
float $get_x[] , $get_y[] , $get_z[] , $xyz[];

yk_PivotEditor_ObjSelect();  //----モデルの自動選択
string $model_name[] = `ls -sl -fl`;
select $sele[0]; 

PolySelectConvert 3;
string $vertex[] = `ls -sl -fl`;

string $x;
for($x in $vertex){
    float $position[] = `xform -q -t -ws $x`;
    $get_x[$i] = $position[0];
    $get_y[$i] = $position[1];
    $get_z[$i] = $position[2];
    $i++;
}

$get_x = `sort $get_x`;
$get_y = `sort $get_y`;
$get_z = `sort $get_z`;
$xyz[0] = (($get_x[0] + $get_x[`size $vertex` - 1]) / 2);
$xyz[1] = (($get_y[0] + $get_y[`size $vertex` - 1]) / 2);
$xyz[2] = (($get_z[0] + $get_z[`size $vertex` - 1]) / 2);

move $xyz[0] $xyz[1] $xyz[2] ($model_name[0] + ".scalePivot") ($model_name[0] + ".rotatePivot");
select $model_name;

}

//-------------------------------------------------------------------------------------------//オリジン原点

global proc yk_PivotEditor_Origin()
{

string $sele[] = `ls -sl -type transform`;
if((`size $sele`) == 0){return;}

string $x;
for($x in $sele){
    move 0 0 0 ($x + ".scalePivot") ($x + ".rotatePivot");
}
    
}

//-------------------------------------------------------------------------------------------//モデル自動選択

global proc yk_PivotEditor_ObjSelect()
{

string $name[] = `ls- sl -fl`; 
if((`size $name`) > 0){
	select -d;
	string $x;
	for($x in $name){
		string $buffer[];
		$size = `tokenize $x "." $buffer`;
		select -add $buffer[0];
	}
}

}

//-------------------------------------------------------------------------------------------//WINDOW

global proc CyPivotEditor()
{

if(`window -q -ex yk_PivotEditor_WINDOW`){deleteUI yk_PivotEditor_WINDOW;}

window -t "Pivot Editor" -toolbox 1 -s 0 yk_PivotEditor_WINDOW;
columnLayout -adjustableColumn true;

rowLayout -nc 3 -cw3 2 175 50;
text  -h 22 -l "";
checkBox -l "Object Center" -v 1 yk_FGH4;
setParent ..;

rowLayout -nc 3 -cw3 2 20 1;
button -w 50 -h 22 -l "X" -c "yk_PivotEditor_Cmds(1 , 1)" -ann "ピボットをモデルの最大Xにします";
button -w 51 -h 22 -l "Y" -c "yk_PivotEditor_Cmds(2 , 1)" -ann "ピボットをモデルの最大Yにします";
button -w 51 -h 22 -l "Z" -c "yk_PivotEditor_Cmds(3 , 1)" -ann "ピボットをモデルの最大Zにします";
setParent..; 

rowLayout -nc 3 -cw3 2 20 1;
button -w 50 -h 22 -l "-X" -c "yk_PivotEditor_Cmds(1 , -1)" -ann "ピボットをモデルの最少Xにします";
button -w 51 -h 22 -l "-Y" -c "yk_PivotEditor_Cmds(2 , -1)" -ann "ピボットをモデルの最少Yにします";
button -w 51 -h 22 -l "-Z" -c "yk_PivotEditor_Cmds(3 , -1)" -ann "ピボットをモデルの最少Zにします";
setParent..; 

rowLayout -nc 3 -cw3 2 20 1;
button -w 77 -h 22 -l "Center" -c "yk_PivotEditor_Center()" -ann "ピボットをモデルの中心にします(Edge 1 選択、Face 1 選択でも可能)";;
button -w 77 -h 22 -l "Origin" -c "yk_PivotEditor_Origin()" -ann "ピボットを原点(0,0,0)にします";;
setParent..; 

separator -height 3 -style "none";

frameLayout -w 70 -l "Coordinate" -bs "out" -pcc "window -e -wh 160 122 yk_PivotEditor_WINDOW" 
                                            -pec "window -e -wh 160 256 yk_PivotEditor_WINDOW" -collapsable 1;  //----フレーム

columnLayout;

rowLayout -nc 3 -cw3 10 20 1;
button -w 153 -h 22 -l "Pivot Get" -c "yk_PivotEditor_Get()" -ann "モデルのピボット座標を表示します。頂点選択でも可能。(複数選択時は中間座標)";
setParent..; 

separator -height 5 -style "none";

rowLayout -nc 3 -cw3 30 10 1;
text  -h 22 -l "  X";
floatField -w 115 -h 22 -v 0 -pre 10 yk_PivotEditor_FLOATFIELD1;
setParent..; 

rowLayout -nc 3 -cw3 30 10 1;
text  -h 22 -l "  Y";
floatField -w 115 -h 22 -v 0 -pre 10 yk_PivotEditor_FLOATFIELD2;
setParent..; 

rowLayout -nc 3 -cw3 30 10 1;
text  -h 22 -l "  Z";
floatField -w 115 -h 22 -v 0 -pre 10 yk_PivotEditor_FLOATFIELD3;
setParent..; 

separator -height 5 -style "none";

rowLayout -nc 3 -cw3 2 20 1;
button -w 75 -h 22 -l "Move" -c "yk_PivotEditor_Move()" -ann "座標値にモデルを移動します";
button -w 75 -h 22 -l "Copy" -c "yk_PivotEditor_Copy()" -ann "座標値にモデルのピボットをコピーします";
setParent..; 

showWindow yk_PivotEditor_WINDOW;
window -e -wh 160 256 yk_PivotEditor_WINDOW;

}
