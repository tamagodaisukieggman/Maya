
global proc yk_UV_Mapping_Tool_Cmd()
{

string $selecm1[] = `filterExpand -sm 34`;  //---face
string $selecm2[] = `filterExpand -sm 35`;  //---UV
if(((`size $selecm1`)==0) && ((`size $selecm2`)==0)){return;}

string $flag;
if(`radioButton -q -sl yk_UV_Mapping_Tool_RADIO2`){
	$flag = "c";
}
else if(`radioButton -q -sl yk_UV_Mapping_Tool_RADIO3`){
	$flag = "x";
}
else if(`radioButton -q -sl yk_UV_Mapping_Tool_RADIO4`){
	$flag = "y";
}
else if(`radioButton -q -sl yk_UV_Mapping_Tool_RADIO5`){
	$flag = "z";
}

PolySelectConvert 1;
polyProjection -ch 1 -type Planar -ibd on -kir -md $flag;
PolySelectConvert 4;

if(`checkBox -q -v yk_UV_Mapping_Tool_CHECK1`){  //----自動調整がオンの場合
	yk_UV_Mapping_Tool_Cmd2();
}

}

//------------------------------------------------------------------------------//関数

global proc yk_UV_Mapping_Tool_Cmd2()
{

string $uv[] = `ls -sl -fl -l`;
if((`size $uv`)==0){return;}
string $uv_c[] = `polyListComponentConversion -tuv $uv`;
float $uv_xy[] = `polyEditUV -q $uv_c[0]`;

/////////////////////////////////////////////////////////textureのサイズを調べる

string $Shader[] = selectMaterial();
string $layed[] = `listConnections -d false -type "layeredTexture" $Shader[0]`;  //----レイヤーテクスチャノード取得
string $material[] = `listConnections -d false -type file $Shader[0]`;  //----テクスチャノード取得

if((`size $layed`) > 0){   
    $material = `listConnections -d false -type file $layed[0]`;  //----レイヤーテクスチャノード取得
    string $bake = "bake";
    int $tu = `gmatch $material[0] ("\*" + $bake + "\*")`;
    if($tu == 1){
        $material[0] = $material[1];
    }        
}
if((`size $layed`) == 0 && (`size $material`) == 0){return;}  //----テクスチャノードがないマテリアルは終了

string $mattex[];
tokenize material[0] "." $mattex;
int $tex_X = `getAttr ($material[0] + ".outSizeX")`;  //----テクスチャサイズ　X 取得
int $tex_Y = `getAttr ($material[0] + ".outSizeY")`;  //----テクスチャサイズ　Y 取得

/////////////////////////////////////////////////////////uvの距離を調べる

float $tex_hiritsu = float($tex_X) / float($tex_Y);

if($tex_hiritsu==1){}  //----まずtex比率分スケールしておく
else if($tex_hiritsu == 2 || $tex_hiritsu == 4){
	polyEditUV -pu $uv_xy[0] -pv $uv_xy[1] -su (1 / $tex_hiritsu);
}
else if($tex_hiritsu == 0.5 || $tex_hiritsu == 0.25){
	polyEditUV -pu $uv_xy[0] -pv $uv_xy[1] -su (1 / $tex_hiritsu);
}
else{
    warning "未対応なテクスチャサイズのため、自動調整されませんでした";
}

select $uv;
string $c_uv[] = `ls -sl -fl`;
string $full_uv[] = {$c_uv[0] , $c_uv[1]};
select $full_uv;

float $scale;
$scale = yk_UV_Mapping_Tool_Pixel_seach(1);

select $uv;
SelectUVShell;
if($scale == 0){return;}
polyEditUV -pu $uv_xy[0] -pv $uv_xy[1] -su (1 / $scale) -sv (1 / $scale);  //----解像度分スケール

}

//------------------------------------------------------------------------------//マテリアル選択

global proc string[] selectMaterial()
{
    string $uvs[] = `ls -sl`;
    string $faces[] = `polyListComponentConversion -tf $uvs`;
    $faces = `ls -fl $faces`;
    string $materials[];
    for($face in $faces){
        $sg = `listSets -o $face`;
		 $mat = `listConnections ($sg[0] + ".surfaceShader")`;
		 if (stringArrayContains($mat[0] , $materials) == false){
			 stringArrayInsertAtIndex(0 , $materials , $mat[0]);		
		 }
	 }	
	 return $materials;
}

//------------------------------------------------------------------------------//アンフォールド

global proc yk_UV_Mapping_Tool_Unfold( int $yk)
{

string $se[] = `ls -sl`;
if((`size $se`) > 0){
	PolySelectConvert 4;
	int $i;
	if($yk == 1){
		$i = 0;
	}
	else if($yk == 2){
		$i = 2;
	}
	else if($yk == 3){
		$i = 1;
	}
	unfold -i 5000 -ss 0.001 -gb 0.0 -gmb 0.5 -pub 0 -ps 0 -oa $i -us off; 	
	if(`checkBox -q -v yk_UV_Mapping_Tool_CHECK1`){  //----自動調整がオンの場合
		yk_UV_Mapping_Tool_Cmd2();
	}
}

}

//------------------------------------------------------------------------------//ピクセルサイクル

global proc yk_UV_Mapping_Tool_Pixel()
{

int $value = `button -q -l yk_UV_Mapping_Tool_INT1`;

if($value == 16){
	button -e -w 38 -l 512 yk_UV_Mapping_Tool_INT1;
}
else{
	button -e -w 38 -l ($value/2) yk_UV_Mapping_Tool_INT1;
}
yk_UV_Mapping_Tool_Save();  //----セーブ

}

//------------------------------------------------------------------------------//トランスサイクル

global proc yk_UV_Mapping_Tool_Move()
{

$yk_UV_Mapping_Tool_INT2value =`button  -q  -l  yk_UV_Mapping_Tool_INT2`;

if($yk_UV_Mapping_Tool_INT2value == 1.0){
	button  -e -l 0.5 yk_UV_Mapping_Tool_INT2;
}
else if($yk_UV_Mapping_Tool_INT2value == 0.5){
	button  -e -l 0.25 yk_UV_Mapping_Tool_INT2;
}
else if($yk_UV_Mapping_Tool_INT2value == 0.25){
	button  -e -l (0.125) yk_UV_Mapping_Tool_INT2;
}
else if($yk_UV_Mapping_Tool_INT2value == 0.125){
	button  -e -l (0.0625) yk_UV_Mapping_Tool_INT2;
}
else if($yk_UV_Mapping_Tool_INT2value == (0.0625)){
	button  -e -l 1  yk_UV_Mapping_Tool_INT2;
}
button -e -w 51 yk_UV_Mapping_Tool_INT2;

}

//■-------------------------------------------------------------------------//ピクセルサーチ

global proc float yk_UV_Mapping_Tool_Pixel_seach(int $flag)
{

float $uv_X[] , $uv_Y[];
string $uv[] = `ls -sl -fl -type "float2"`;

if((`size $uv`) != 2){ //----2点選択でなければ終了
    button -e -w 230 -bgc 0.831 0.816 0.784 -l ("  UVが"+ (`size $uv`) +"点選択されています　計測する2点を選択してください") yk_UV_Mapping_Tool_BUTTON2;
    return 0;
}

/////////////////////////////////////////////////////////textureのサイズを調べる

string $Shader[] = selectMaterial();
string $layed[] = `listConnections -d false -type "layeredTexture" $Shader[0]`;  //----レイヤーテクスチャノード取得
string $material[] = `listConnections -d false -type file $Shader[0]`;  //----テクスチャノード取得
if((`size $layed`) > 0){   
    $material = `listConnections -d false -type file $layed[0]`;  //----レイヤーテクスチャノード取得
    string $bake = "bake";
    int $tu = `gmatch $material[0] ("\*" + $bake + "\*")`;
    if($tu == 1){
        $material[0] = $material[1];
    }     
}
if((`size $layed`) == 0 && (`size $material`) == 0){return 0;}  //----テクスチャノードがないマテリアルは終了

string $mattex[];
tokenize material[0] "." $mattex;
int $tex_X = `getAttr ($material[0] + ".outSizeX")`;  //----テクスチャサイズ　X 取得
int $tex_Y = `getAttr ($material[0] + ".outSizeY")`;  //----テクスチャサイズ　Y 取得

/////////////////////////////////////////////////////////uvの距離を調べる

float $get_uv1[] , $get_uv2[];
float $tex_hiritsu = float($tex_X) / float($tex_Y);

if($tex_hiritsu == 1 || $tex_hiritsu == 0.5 || $tex_hiritsu == 0.25 || $tex_hiritsu == 2 || $tex_hiritsu == 4){  //----比率を見る
	polyEditUV -sv (1 / $tex_hiritsu);  //----比率分UVのスケールをする
	float $get_uv1[] = `polyEditUV -q $uv[0]`;  //----その値を取得
	float $get_uv2[] = `polyEditUV -q $uv[1]`;
	polyEditUV -sv $tex_hiritsu;  //----取得後スケールを元に戻す
	$uv_X[0] = $get_uv1[0];
	$uv_X[1] = $get_uv2[0];
	$uv_X = `sort $uv_X`;
	$uv_Y[0] = $get_uv1[1];
	$uv_Y[1] = $get_uv2[1];
	$uv_Y = `sort $uv_Y`;
}
else{  //---比率外は終了
	button -e -w 230 -l ("未対応のテクスチャサイズです") -bgc 1 1 1  yk_UV_Mapping_Tool_BUTTON2;
	return 0;
}

float $aa = ($uv_X[1] - $uv_X[0]) * ($uv_X[1] - $uv_X[0]);
float $bb = ($uv_Y[1] - $uv_Y[0]) * ($uv_Y[1] - $uv_Y[0]);
float $uv_hiritsu = sqrt ($aa + $bb);  //----UV比率値取得（0〜1の値）
if($uv_hiritsu == 0){
    $uv_hiritsu+=0.00001;
}
print ("UV比率　"+$uv_hiritsu);
/////////////////////////////////////////////////////////vertexの距離を調べる

string $vertex1[] = `polyListComponentConversion -tv $uv[0]`;  //----uv1をvertex1に変換
string $vertex2[] = `polyListComponentConversion -tv $uv[1]`;  //----uv2をvertex2に変換
vector $vector1 = `pointPosition -w $vertex1[0]`;  //----vertex1の座標を取得
vector $vector2 = `pointPosition -w $vertex2[0]`;  //----vertex2の座標を取得
float $vertex_distance = `mag ($vector1 - $vector2)`;  //----vertex間の距離の取得
if(`currentUnit -query -linear` == "cm"){   	
   $vertex_distance = $vertex_distance * 0.01; 
}

print (" 　　　　頂点距離　"+$vertex_distance);

/////////////////////////////////////////////////////////

int $pixel = `button -q -l yk_UV_Mapping_Tool_INT1`;

int $genzai = ($tex_X / (1 / $uv_hiritsu));  //----現在pixel取得
int $tekisetsu = $vertex_distance * $pixel;  //----適切pixel取得
float $size = (1 / $uv_hiritsu);
float $scale = ($tex_X /$size) / ($vertex_distance * $pixel);
$scale = yk_UV_Mapping_Tool_SISYA($scale , 4);  //----四捨五入の数値にする

print (" 　　　　現在　"+$genzai);
print (" 　　　　適切　"+$tekisetsu);
print (" 　　　　スケール　"+$scale);
print ("@"+$scale);
if($flag == 1){return $scale;}  //----■

button -e -w 230 -l (" 現在:"+$genzai+"Pix" +"   " +"適切:"+$tekisetsu+"Pix"+"   "+$scale) yk_UV_Mapping_Tool_BUTTON2;

float $a = $genzai;  //----floatにキャスト
float $b = $tekisetsu;
$resize = yk_UV_Mapping_Tool_SISYA(($b / $a) , 4);  //----四捨五入の数値にする
floatField -e -v $resize yk_UV_Mapping_Tool_FloatField;

float $rgb[];
if ($genzai >= ($tekisetsu * 1.2)){  //----最後比率のカラー表示
    $rgb = {1.0 , 0.8 , 0.8};
}
else if($genzai <= ($tekisetsu * 0.8)){
    $rgb = {0.8 , 0.8 , 1.0};
}
else{
    $rgb = {1.0 , 1.0 , 1.0};
}
button -e -w 230 -l (" 現在:"+$genzai+"Pix" +"   " +"適切:"+$tekisetsu+"Pix"+"   "+$scale) -bgc $rgb[0] $rgb[1] $rgb[2] yk_UV_Mapping_Tool_BUTTON2;
return 0;

}

//-------------------------------------------------------------------------//UVリサイズ

global proc yk_UV_Mapping_Tool_Resize()
{

string $sele[] = `ls -sl -type float2`;
if((`floatField -q -v yk_UV_Mapping_Tool_FloatField`) != 0){
	if((`size $sele`) > 0){
		float $get = `floatField -q -v yk_UV_Mapping_Tool_FloatField`;
		SelectUVShell;
		polyMoveUV -su $get -sv $get;
	}
}

}

//----------------------------------------------------------------------------------//四捨五入サンプル

global proc float  yk_UV_Mapping_Tool_SISYA (float $inValue, int $precision)
{
           int $negative = 0;
           if ($inValue < 0.0){
                      $inValue = -$inValue;
                      $negative = 1;
           }// ----Example: yk_Transform_Clean_com2 1.26456 2

           $adjuster = 5.0 / pow (10,$precision);// ----$adjuster = 0.05
           $gain = pow (10, ($precision-1));// ----$gain = 10
           $inValue = $inValue + $adjuster;//----1.26456 + 0.05 = 1.31456
           $inValue = $inValue * $gain;// ----1.26456 * 10 = 13.1456
           $inValue = floor($inValue);//---- floor(13.1456) = 13
           $inValue = $inValue / $gain;// ----13 / 10 = 1.3

           if ($negative){
                       $inValue = -$inValue;
           }
           return $inValue;
}

//----------------------------------------------------------------//セーブ

global proc yk_UV_Mapping_Tool_Save()
{

string $myScriptDir = `internalVar -userScriptDir`;
$CBC_listId = `fopen ($myScriptDir +"/"+"yk_UV_Mapping_Tool"+".txt") "w"`;
fopen ($myScriptDir +"/"+"yk_UV_Mapping_Tool" + ".txt") "a";

int $kei = `button -q -l yk_UV_Mapping_Tool_INT1`;
fprint $CBC_listId $kei;
fprint $CBC_listId ",";
fprint $CBC_listId "old";

fclose $CBC_listId;

}

//-----------------------------------------------------------------------//ロード

global proc yk_UV_Mapping_Tool_Lord()
{

int $CBC_listId;
string $myScriptDir = `internalVar -userScriptDir`;
int $zo = `filetest -f ($myScriptDir+"/yk_UV_Mapping_Tool.txt")`;
if($zo == 1){
	$CBC_listId = `fopen($myScriptDir +"/"+ "yk_UV_Mapping_Tool" + ".txt") "r"`;
	string  $text = `fread $CBC_listId $text`;
	string $buffer[];
	int $buffer_size = `tokenize $text "," $buffer`;

	button -e -l $buffer[0] yk_UV_Mapping_Tool_INT1;
	button -e -w 38 yk_UV_Mapping_Tool_INT1;
}

fclose $CBC_listId;

}

//------------------------------------------------------------------------------//WINDOW

global proc CyUVMapping_Tool()
{

if(`window -q -ex yk_UV_Mapping_Tool_WINDOW`){deleteUI yk_UV_Mapping_Tool_WINDOW;}

window -t "UV Mapping Tool" -toolbox 1 -s 1 yk_UV_Mapping_Tool_WINDOW;
columnLayout -adjustableColumn true ;


frameLayout -label " Development UV" -borderStyle "etchedIn";
columnLayout;

rowLayout -nc 4 -cw4 1 175 250 50;
text -l "";
text -l " Auto Adjusts UVsize";
checkBox -l  "On" -v 1 yk_UV_Mapping_Tool_CHECK1;
text -l "";
setParent ..;

separator -height 6 -style "none";

rowLayout -nc 6 -cw6 3 54 67 35 35 38;
text -l "";
radioCollection;
radioButton -label "None" -select yk_UV_Mapping_Tool_RADIO1;
radioButton -label "Camera" yk_UV_Mapping_Tool_RADIO2;
radioButton -label "X" yk_UV_Mapping_Tool_RADIO3;
radioButton -label "Y" yk_UV_Mapping_Tool_RADIO4;
radioButton -label "Z" yk_UV_Mapping_Tool_RADIO5;
setParent ..;

separator -height 5 -style "none";

rowLayout -nc 2 -cw2 2 150;
text -l "";
button -w 230 -h 22 -l "Apply" -c"yk_UV_Mapping_Tool_Cmd()";
setParent..; 

separator -height 6 -style "none";

rowLayout -nc 6 -cw6 2 150 40 10 5 5;
text -l "";
text  -h 22 -l " Texture size 1m  =";
button -w 38 -h 22 -l 128 -bgc 1 1 1 -c "yk_UV_Mapping_Tool_Pixel()" yk_UV_Mapping_Tool_INT1;
text -l " Pixel";
setParent..; 

separator -height 6 -style "none";
separator -hr true -w 250 -style "in";
separator -height 6 -style "none";

rowLayout -nc 4 -cw4 2 68 68 68;
text -l "";
button -w 75 -h 22 -l "Unfold" -c "yk_UV_Mapping_Tool_Unfold(1)" -ann "U+Vにアンフォードを実行します";
button -w 75 -h 22 -l "U" -c "yk_UV_Mapping_Tool_Unfold(2)" -ann "Uのみにアンフォードを実行します";
button -w 76 -h 22 -l "V" -c "yk_UV_Mapping_Tool_Unfold(3)" -ann "Vのみにアンフォードを実行します";

setParent ..;
separator -height 6 -style "none";
setParent ..;setParent ..;

separator -height 6 -style "none";

rowLayout -nc 3 -cw3 4 188 30;
text -l "";
button -w 230 -h 22 -l "Pixel Search"-c "yk_UV_Mapping_Tool_Pixel_seach(0)" -ann "UV間の現在のピクセルと適切なピクセルを表示します" yk_UV_Mapping_Tool_BUTTON2;
setParent..; 

separator -height 4 -style "none";

rowLayout -nc 6 -cw6 3 36 60 43 100 5;
text -l "";
text  -h 22 -l" Scale";
floatField -w 60 -h 22 -pre 3 -v 0 -min -100 yk_UV_Mapping_Tool_FloatField;
button -w 131 -h 22 -l "UV Resize" -c "yk_UV_Mapping_Tool_Resize()" -ann "現在pixを適切pixにするUVスケール値にUVSellでスケールされます" yk_UV_Mapping_Tool_BUTTON3;
setParent..; 

separator -height 6 -style "none";

showWindow yk_UV_Mapping_Tool_WINDOW;
window -e -wh 246 243 yk_UV_Mapping_Tool_WINDOW;

yk_UV_Mapping_Tool_Lord();  //----ロード
}

