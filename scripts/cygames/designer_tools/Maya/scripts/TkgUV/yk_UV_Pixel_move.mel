//-----------------------------------------------------------------------//texサーチ

global proc float[] yk_UV_Pixel_move_size()
{

float $tex_XY[];
string $face[] = `ls -sl -fl`;
if((`size $face`) == 0){return $tex_XY;}
$sv = `polyListComponentConversion -tf $face[0]`;
$connectSG = `listSets -type 1 -object $sv[0]`;
$Shader = `listConnections -d off ($connectSG[0] + ".surfaceShader")`;
string $mattex[] = `listConnections -d false -type file $Shader`;

if((`size $mattex`) > 0){
	$tex_XY[0] = `getAttr ($mattex[0] + ".outSizeX")`; 
	$tex_XY[1] = `getAttr ($mattex[0] + ".outSizeY")`; 
}
    return $tex_XY;
}

//-----------------------------------------------------------------------//UV  ピクセル移動

global proc yk_UV_Pixel_move_Cmd1(int $x)
{

float $tex_XY[] = yk_UV_Pixel_move_size();
if($tex_XY[0] == 0 || $tex_XY[1] == 0){return;}

float $yk_t1v = `intField -q -v yk_UV_Pixel_move_FIELD1`;
float $trans_u = ((1 / $tex_XY[0]) * $yk_t1v);
float $trans_v = ((1 / $tex_XY[1]) * $yk_t1v);
if($x == 1){
	polyEditUV -u $trans_u -v 0;
}
else if($x == -1){
	polyEditUV -u (-1 * $trans_u) -v 0;
}
else if($x == 2){
	polyEditUV -u 0 -v $trans_v;
}
else if($x == -2){
	polyEditUV -u 0 -v (-1 * $trans_v);
}

}

//-----------------------------------------------------------------------//　近くのピクセルにスナップ

global proc yk_UV_Pixel_move_Cmd2(int $flag)
{

float $tex_XY[] = yk_UV_Pixel_move_size();
if($tex_XY[0] == 0 || $tex_XY[1] == 0){return;}
float $kei1 = (1 / (1 / $tex_XY[0]));
float $kei2 = (1 / (1 / $tex_XY[1]));
polyGridUV $kei1 $kei2 $flag;

}

//-------------------------------------------------------------//ウインドウ

global proc yk_UV_Pixel_move()
{

if(`window -q -ex yk_UV_Pixel_move_WINDOW`){deleteUI yk_UV_Pixel_move_WINDOW;}

window -t "UV_Pixel_move" -toolbox 1 -s 1 yk_UV_Pixel_move_WINDOW;
columnLayout;

separator -height 5 -style "none";

rowLayout -nc 5 -cw5 53 100 50 50 50 ;
text -l"";
button -w 50 -h 22 -l "↑" -c "yk_UV_Pixel_move_Cmd1(2)";
setParent ..;

rowLayout -nc 5 -cw5 3 50 48 50 50 ;
text -l"";
button -w 50 -h 22 -l "←" -c "yk_UV_Pixel_move_Cmd1(-1)";
intField -w 48  -min 1 -max 512 -v 1 yk_UV_Pixel_move_FIELD1;
button -w 50 -h 22 -l "→" -c "yk_UV_Pixel_move_Cmd1(1)";
setParent ..;

rowLayout -nc 5 -cw5 53 50 50 50 50 ;
text -l"";
button -w 50 -h 22 -l "↓" -c "yk_UV_Pixel_move_Cmd1(-2)";
setParent ..;

separator -height 5 -style "none";

rowLayout -nc 5 -cw5 3 75 50 50 50 ;
text -l"";
button -w 75 -h 22 -l "Corner snap" -c "yk_UV_Pixel_move_Cmd2(0)" -bgc 1 0.8 0.8;
button -w 75 -h 22 -l "Center snap" -c "yk_UV_Pixel_move_Cmd2(1)" -bgc 0.8 1 0.8;
setParent ..;

showWindow yk_UV_Pixel_move_WINDOW;
window -e -wh 165 113 yk_UV_Pixel_move_WINDOW;

}



