//---------------------------------------------------------ロード

global proc CyShelf_Editor_Load(int $set)
{

menuItem -e -l "Load Set 1" CyShelf_Editor_Item_l1;  
menuItem -e -l "Load Set 2" CyShelf_Editor_Item_l2; 
menuItem -e -l "Load Set 3" CyShelf_Editor_Item_l3;
menuItem -e -l ("Load Set " + $set + " ★") ("CyShelf_Editor_Item_l" + $set);

int $file_1 , $file_2;
string $buffer_icon[], $buffer_para[] , $buffer_winodw_size[] , $langu;
string $myDir = `internalVar -userScriptDir`;

$file_1 = `fopen ($myDir + "/" + "/CyShelf_Editor_Savefile_" + $set + ".txt") "r"`;
string $exampleFileName = ($myDir + "CyShelf_Editor_Savefile_" + $set + ".txt");
string $save_icon = freadAllText($exampleFileName);   
if((`size $save_icon `) == 0){return;}
//string $save_icon = `fread $file_1 $save_icon`; //----1024バイト制限があるので不使用

int $size = `tokenize $save_icon "◎" $buffer_icon`;
string $x;
for($x in $buffer_icon){
    int $size = `tokenize $x "◆" $buffer_para`;
    string $type = `substring $buffer_para[3] 1 6`;//----言語指定（importがあれば）
    if($type == "import"){
       $langu = "python";
    }
    else{
        $langu = "mel";
	}
    if($buffer_para[2] == " "){
        shelfButton -rpt true -stp $langu -i1 $buffer_para[0] -l $buffer_para[1] -c $buffer_para[3] -ann $buffer_para[4] -p "CyShelf_Editor_TAB";   
    }
    else{
        shelfButton -rpt true -stp $langu -i1 $buffer_para[0] -l $buffer_para[1] -iol $buffer_para[2] -c $buffer_para[3] -ann $buffer_para[4] -p "CyShelf_Editor_TAB";              
    } 
}
fclose $file_1;

$file_2 = `fopen ($myDir + "/" + "/CyShelf_Editor_Save_window_" + $set + ".txt") "r"`;//----window
string $save_icon_size = `fread $file_2$save_icon_size`;
int $size = `tokenize $save_icon_size "," $buffer_winodw_size`;
float $w = $buffer_winodw_size[0];//----テキストの数字をfloat型に変換
float $h = $buffer_winodw_size[1];//----テキストの数字をfloat型に変換
window -e -wh $w $h CyShelf_Editor_WINDOW;	

fclose $file_2;
      
}

//---------------------------------------------------------セーブ

global proc CyShelf_Editor_Save()
{
string $star1 , $star2 , $star3;
int $set;
string $star1 = `menuItem -q -l CyShelf_Editor_Item_l1`;//----セーブ先表示の処理
string $star2 = `menuItem -q -l CyShelf_Editor_Item_l2`;
string $star3 = `menuItem -q -l CyShelf_Editor_Item_l3`;
if(`gmatch $star1 ("\*" + "★" + "\*")` == 1){    
    $set = 1;
}
if(`gmatch $star2 ("\*" + "★" + "\*")` == 1){    
    $set = 2;
}
if(`gmatch $star3 ("\*" + "★" + "\*")` == 1){    
    $set = 3;
}
string $save1 =` confirmDialog -t "Confirm" -m ("Set " + $set + "  にSaveしてよろしいですか?") -b "Yes" -b "No" `;
if($save1 == "No"){return;} 
 
string $shelf_name[] =`shelfLayout -q -ca "CyShelf_Editor_TAB"`;
string $myDir = `internalVar -userScriptDir`;
if((`size $shelf_name`) > 0){
    shelfButton -q -l $shelf_name[0];   
    int $file_1 = `fopen ($myDir + "/" + "CyShelf_Editor_Savefile_" + $set + ".txt") "w"`;
    fopen ($myDir + "/" + "CyShelf_Editor_Savefile_" + $set + ".txt") "a";
    
    string $x;
    for($x in $shelf_name){
        string $Iconname = `shelfButton -q -i1 $x`;//----イメージ
        string $name = `shelfButton -q -l $x`;//----名前
        string $IOL = `shelfButton -q -iol $x`;//----オーバーレイ名前
        string $comamnd_1 = `shelfButton -q -c $x`;//----コマンド内容
        string $ann_1 = `shelfButton -q -ann $x`;//----説明
        
        if(`size $Iconname` > 500 || `size $name` > 500 || `size $IOL` > 500 || `size $comamnd_1` > 500 || `size $ann_1` > 500){//----容量でかいコマンドがあるとストップ
            confirmDialog -t "" -m "長文コマンドのシェルフが含まれているのでセーブできませんでした" -b "OK";
            continue;
        }
        
        if(`size $Iconname` == 0){$Iconname = " ";}
        if(`size $name` == 0){$name = " ";}
        if(`size $IOL` == 0){$IOL = " ";}
        if(`size $comamnd_1` == 0){$comamnd_1 = " ";}
        if(`size $ann_1` == 0){$ann_1 = " ";}
        
        fprint $file_1 ($Iconname + "◆");
        fprint $file_1 ($name + "◆" );
        fprint $file_1 ($IOL + "◆");
        int $a = `gmatch $comamnd_1 ("\*" + "mel.eval(" + "\*")`;//----.melがあるとエラーでるので文字列から削除
        if($a == 1){
            string $new = `substitute "mel.eval" $comamnd_1 "eval"`;
            fprint $file_1 ($new + "◆");
        }
        else{
            fprint $file_1 ($comamnd_1 + "◆");//----アイコン単位改行
        }
        fprint $file_1 ($ann_1 + "◎");//----アイコン単位改行
    }
    fclose $file_1;
}

int $file_2 = `fopen ($myDir + "/" + "CyShelf_Editor_Save_window_" + $set + ".txt") "w"`;
fopen ($myDir + "/" + "CyShelf_Editor_Save_window_" + $set + ".txt") "a";
int $window_size[] = `window -q -wh CyShelf_Editor_WINDOW`;
fprint $file_2 ($window_size[0] + "," + $window_size[1] );  

fclose $file_2;

}

//---------------------------------------------------------初期化

global proc CyShelf_Editor_Reset()
{

string $myDir = `internalVar -userScriptDir`;

int $i = 1;
for($i = 1 ; $i < 4 ; $i++){
    if(`filetest -f ($myDir + "/CyShelf_Editor_Savefile_" + $i + ".txt")` == 0){
        int $file_1 = `fopen ($myDir + "/" + "/CyShelf_Editor_Savefile_" + $i + ".txt") "w"`;
        fprint $file_1 "polySphere.png◆球◆ ◆polySphere -r 1 -sx 20 -sy 20 -ax 0 1 0 -cuv 2 -ch 1; objectMoveCommand;◆球◎";
        fclose $file_1;  
    } 
    if(`filetest -f ($myDir + "/CyShelf_Editor_Save_window_" + $i + ".txt")` == 0){
        int $file_1 = `fopen ($myDir + "/" + "/CyShelf_Editor_Save_window_" + $i + ".txt") "w"`;
        fprint $file_1 "100,100";
        fclose $file_1;  
    }  
}

}

//---------------------------------------------------------window

global proc CyShelf_Editor(int $flag)
{

if(`window -q -ex CyShelf_Editor_WINDOW`){deleteUI CyShelf_Editor_WINDOW;}    
window -w 30 -h 100 -t "CyShelf_Editor" -toolbox 1 -menuBar 1 CyShelf_Editor_WINDOW;

menu -l "Layout" ;
menuItem -l "Save Set" -c "CyShelf_Editor_Save()" CyShelf_Editor_Item_s1;
menuItem -divider true;
menuItem -l "Load Set 1 ★" -c "CyShelf_Editor(0); CyShelf_Editor_Load(1)"CyShelf_Editor_Item_l1;  
menuItem -l "Load Set 2" -c "CyShelf_Editor(0); CyShelf_Editor_Load(2)"CyShelf_Editor_Item_l2; 
menuItem -l "Load Set 3" -c "CyShelf_Editor(0); CyShelf_Editor_Load(3)"CyShelf_Editor_Item_l3;

shelfLayout  -style "iconOnly" -bgc 0.26 0.26 0.26 "CyShelf_Editor_TAB";
setParent ..;setParent ..;

showWindow CyShelf_Editor_WINDOW;

CyShelf_Editor_Reset();//----window初期化

if($flag == 1){
    CyShelf_Editor_Load(1);
}

}