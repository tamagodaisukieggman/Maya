//■yk_DisplayEditor _
//■16/01/28　Version

//------------------------------------------------------------------------------//コマンド集

global proc yk_DisplayEditor_Cmds(int $No , string $color , string $name)
{

string $panel = yk_DisplayEditor_Panel();  //----フォーカスパネル取得
float $button_1[];
if($No != 8 && $No != 11){
    $button_1 = `button -q -bgc $name`;  //----ボタンの色取得（オンオフの取得で使う）
}

switch($No){

case 1:  //----表示ディフューズ
    polyOptions -global -cs 0 -cm diffuse;
    modelEditor -e -displayAppearance "smoothShaded" -displayTextures 1 -displayLights "default" $panel;
    break;
case 22:  //----表示テクスチャ
    polyOptions -global -cs 0 -cm diffuse;
    modelEditor -e -displayAppearance "smoothShaded" -displayTextures 1 -displayLights "flat" $panel;
    break;
case 2:  //----表示頂点カラー
    polyOptions -global -cs 1 -mb "overwrite" -cm ambientDiffuse;
    modelEditor -e -displayAppearance "smoothShaded" -displayTextures 0 -displayLights "none" $panel;
    break;
case 3:  //----テクスチャ　＆　VC
    polyOptions -global -cs 1 -mb "multiply" -cm diffuse;
    modelEditor -e -displayAppearance "smoothShaded" -displayTextures 1 -displayLights "flat" $panel;
    break;
case 4:  //----ノーマル表示
    polyOptions -global -cs 0 -cm emission;
    modelEditor -e -displayAppearance "smoothShaded" -displayTextures 0 -displayLights "default" $panel;
    break;
case 5:  //----ワイヤーフレーム
    modelEditor -e -displayAppearance "wireframe" -displayTextures 0 -displayLights "default" $panel;
    break;
case 6:  //----ポリゴンボーダー
    if($button_1[0] < 0.8){
        polyOptions -global -db 0 (`listTransforms -g`);
    }
    else{
        yk_DisplayEditor_Geometry_color(1);//----オフに
        polyOptions -global -db 2 (`listTransforms -g`);
    }
    break;
case 7:  //----UVボーダー
    if($button_1[0] < 0.8){
        polyOptions -displayMapBorder 0 (`listTransforms -g`);
    }
    else{
        yk_DisplayEditor_Geometry_color(1);//----オフに
        polyOptions -displayMapBorder 1 (`listTransforms -g`);
    }
    break;
case 23:  //----ハードエッジ
    if($button_1[0] < 0.8){
        polyOptions -allEdges  (`listTransforms -g`);
    }
    else{
        yk_DisplayEditor_Geometry_color(1);//----オフに
        polyOptions -hardEdge  (`listTransforms -g`);
    }
    break;
case 8:  //----強さ1
    float $value8 = `floatSlider -q -v yk_DisplayEditor_SET8`;
    $value8 = $value8 + 0.01;
    if($value8 >= 10){
        $value8 = 10;
    }
    floatSlider -e -v $value8 yk_DisplayEditor_SET8;
    polyOptions -sb ($value8) (`listTransforms -g`);
    break;
case 9:  //----頂点法線
    if($button_1[0] < 0.8){
        polyOptions -dn 0 -pt (`listTransforms -g`);
    }
    else{
        polyOptions -dn 1 -pt (`listTransforms -g`);
    }
    break;
case 10:  //----面法線
    if($button_1[0] < 0.8){
        polyOptions -dn 0 -f (`listTransforms -g`);
    }
    else{
        polyOptions -dn 1 -f (`listTransforms -g`);
    }
    break;
case 11:  //----強さ2
    float $value11 = `floatSlider -q -v yk_DisplayEditor_SET11`;
    $value11 = $value11 + 0.01;
    if($value11 >= 3){
        $value11 = 3;
    }
    floatSlider -e -v $value11 yk_DisplayEditor_SET11;
    polyOptions -sn ($value11) (`listTransforms -g`);
    break;
case 12:  //----バッカリング
    if($button_1[0] < 0.8){
        polyOptions -bc (`listTransforms -g`);
    }
    else{
        polyOptions -fb (`listTransforms -g`);
    }
    break;
case 13:  //----アイソレート
    if($button_1[0] < 0.8){
        if(`isolateSelect -q -state $panel`){
            enableIsolateSelect $panel 0;
            isolateSelect -state 0 $panel;
        }
    }
    else{
        enableIsolateSelect $panel 1;
        isolateSelect -state 1 $panel;
    }
    break;
case 14:  //----オーバーワイヤー
    if($button_1[0] < 0.8){
        if(`modelEditor -q -wos $panel`){
            modelEditor -e -wos 0 $panel;
        }
    }
    else{
        modelEditor -e -wos 1 $panel;
    }
    break;
case 15:  //----UV シェル選択
    break;
case 24:  //----ジョイント表示
    if($button_1[0] < 0.8){
        modelEditor -e -joints 0 $panel;
    }
    else{
        modelEditor -e -joints 1 -jx 1 $panel;

    }
    break;
case 16:  //----トライアングル
    if($button_1[0] < 0.8){
        polyOptions -dt 0 (`listTransforms -g`);
    }
    else{
        polyOptions -dt 1 (`listTransforms -g`);
    }
    break;
case 17:  //----インスタンス選択
    color `ls`;  //----色の初期化
    string $sele[] = `ls -sl`;
    string $sele_transfrom[] = `ls -tr`;
    select -d;
    string $x , $get[];
    int $count = 0;
    if(size($sele_transfrom) > 0){  //----インスタンス選択
        for($x in $sele_transfrom){
            string $sele_s[] = `listRelatives -s -f -ni $x`;
            string $sele_ap[] = `listRelatives -ap -f -ni $sele_s`;
            if( size($sele_ap) > 1){
                $get[$count] = $x;
                $count++;
            }
        }
    }
    if((`size $get`) > 0){
        select $get;
    }
    if($button_1[0] < 0.8){
        color;
    }
    else{
        color -ud 7;
    }
    select -d;
    if(`size $sele`){select -add $sele;}  //----初めに選択していたもの
    break;
case 18:  //----Bgカラー
    string $flag18 = `text -q -l yk_DisplayEditor_TEXT2`;
    string $v18;
    if($flag18 == "1"){
        $v18 = "2";
    }
    else if($flag18 == "2"){
        $v18 = "3";
    }
    else if($flag18 == "3"){
        $v18 = "4";
    }
    else{
        $v18 = "1";
    }
    text -e -l $v18 yk_DisplayEditor_TEXT2;
    CycleBackgroundColor;
    break;
case 19:  //----UI
    string $flag19 = `text -q -l yk_DisplayEditor_TEXT3`;
    string $v19;
    if($flag19 == "1"){
        setAllMainWindowComponentsVisible 0;
        $v19 = "2";
    }
    else if($flag19 == "2"){
        setAllMainWindowComponentsVisible 0;
        ToggleShelf;
        $v19 = "3";
    }
    else{
        setAllMainWindowComponentsVisible 1;
        $v19 = "1";
    }
    text -e -l $v19 yk_DisplayEditor_TEXT3;
    break;
case 20:  //----X ray
    if($button_1[0] < 0.8){
        modelEditor -e -xray 0 $panel;
    }
    else{
        modelEditor -e -xray 1 $panel;
    }
    break;
case 25:  //----2p距離
    if($button_1[0] < 0.8){
        if (`headsUpDisplay -ex yk_DisplayEditor_DistanceHUD1`){
            headsUpDisplay -rem yk_DisplayEditor_DistanceHUD1;
        }
    }
    else{
        if (`headsUpDisplay -ex yk_DisplayEditor_DistanceHUD1`){
            headsUpDisplay -rem yk_DisplayEditor_DistanceHUD1;
        }
        headsUpDisplay -s 2 -b 0 -bs "large" -l "2point Distance" -lfs "large" -dataFontSize "large" -c "CyDisplayEditor_2p_Distance()" -event  "SelectionChanged" -nodeChanges "attributeChange" yk_DisplayEditor_DistanceHUD1;
    }
    break;
case 21:  //----グリッド
    string $flag21 = `text -q -l yk_DisplayEditor_TEXT4`;
    string $v21 , $lavel , $unit;
    int $grid_size;
    if(`currentUnit -query -linear` == "cm"){//----単位を取得
        $unit = "cm";
    }
    else if(`currentUnit -query -linear` == "m"){
        $unit = "m";
    }
    else{
        $unit = "";
    }
    if($flag21 == "1"){
        $lavel = ("Grid    1" + $unit);
        $grid_size = 1;
        modelEditor -e -grid 1 $panel;
        $v21 = "2";
    }
    else if($flag21 == "2"){
        $lavel = ("Grid   10" + $unit);
        $grid_size = 10;
        modelEditor -e -grid 1 $panel;
        $v21 = "3";
    }
    else if($flag21 == "3"){
        $lavel = ("Grid   50" + $unit);
        $grid_size = 50;
        modelEditor -e -grid 1 $panel;
        $v21 = "4";
    }
    else if($flag21 == "4"){
        $lavel = ("Grid  100" + $unit);
        $grid_size = 100;
        modelEditor -e -grid 1 $panel;
        $v21 = "5";
    }
    else{
        modelEditor -e -grid 0 $panel;
        $grid_size = 1;
        $lavel = "Grid";
        $v21 = "1";
    }
    button -e -w 105 -h 22 -l $lavel yk_DisplayEditor_SET21;
    grid -d 1 -sp $grid_size;
    text -e -l $v21 yk_DisplayEditor_TEXT4;
    break;
}

if($No != 8 && $No != 11){
    yk_DisplayEditor_Color($No , $color , $name);  //----ボタンカラー変更
}

}

//------------------------------------------------------------------------------//現在のパネルを取得

global proc string yk_DisplayEditor_Panel()
{

string $getpanel = `getPanel -withFocus`;
//if($getpanel != "modelPanel1" && $getpanel != "modelPanel2" && $getpanel != "modelPanel3" && $getpanel != "modelPanel4"){
//    $getpanel = "modelPanel4";
//}
return $getpanel;

}

//------------------------------------------------------------------------------//ボタンカラー調整

global proc yk_DisplayEditor_Geometry_color(int $on)
{

if($on == 1){
    polyOptions -allEdges  (`listTransforms -g`);
    polyOptions -global -db 0 (`listTransforms -g`);  //--オフに
    polyOptions -displayMapBorder 0 (`listTransforms -g`);  //--オフに
    button -e -bgc 0.8 1 0.8 yk_DisplayEditor_SET6;
    button -e -bgc 0.8 1 0.8 yk_DisplayEditor_SET7;
    button -e -bgc 0.8 1 0.8 yk_DisplayEditor_SET23;
}

}

//------------------------------------------------------------------------------//ボタンカラー調整

global proc yk_DisplayEditor_Color(int $No , string $color , string $name)
{

float $button_1[] = `button -q -bgc $name`;
string $BgColor = `text -q -l yk_DisplayEditor_TEXT2`;  //----Bgカラー
string $UI = `text -q -l yk_DisplayEditor_TEXT3`;  //----UI
string $Grid = `text -q -l yk_DisplayEditor_TEXT4`;  //----Grid

if($color == "red"){
    int $i;
    for($i = 1 ; $i < 6 ; $i++){
        button -e -bgc 1 0.8 0.8 ("yk_DisplayEditor_SET" + $i);  //----全赤ボタンの色を全部明るくする
        button -e -bgc 1 0.8 0.8 ("yk_DisplayEditor_SET22");  //----全赤ボタンの色を全部明るくする
    }
    button -e -bgc 0.8 0.6 0.6 $name;  //----自赤ボタンを暗くする
}
else if($color == "green"){
    if($No == 6){  //----ポリゴンボーダー
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
        }
    }
    if($No == 7){  //----UVボーダー
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
        }
    }
    if($No == 23){  //----ハードエッジ
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
        }
    }
    if($No == 9){  //---頂点法線
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
           return;
        }
        button -e -bgc 0.8 1 0.8 "yk_DisplayEditor_SET10";  //----面法線 ボタン色を元に戻す
    }
    if($No == 10){  //----面法線
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
           return;
        }
        button -e -bgc 0.8 1 0.8 "yk_DisplayEditor_SET9";  //----頂点法線 ボタン色を元に戻す
    }
    if($No == 12){  //----バッカリング
        button -e -bgc 0.6 0.8 0.6 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 1 0.8 $name;  //----自ボタン色を元に戻す
        }
    }
}
else if($color == "blue"){
    if($No == 18){  //----Bgカラー
        button -e -bgc 0.6 0.6 0.8 $name;
        if($BgColor == 1){
           button -e -bgc 0.8 0.8 1 $name;  //----自ボタン色を元に戻す
        }
        return;
    }
    else if($No == 19){  //----UI
        button -e -bgc 0.6 0.6 0.8 $name;
        if($UI == 1){
           button -e -bgc 0.8 0.8 1 $name;  //----自ボタン色を元に戻す
        }
        return;
    }
   else if($No == 21){  //----Grid
        button -e -bgc 0.6 0.6 0.8 $name;
        if($Grid == 1){
           button -e -bgc 0.8 0.8 1 $name;  //----自ボタン色を元に戻す
        }
        return;
    }
    else{
        button -e -bgc 0.6 0.6 0.8 $name;
        if($button_1[0] < 0.8){
           button -e -bgc 0.8 0.8 1 $name;  //----自ボタン色を元に戻す
        }
    }
}

}

//------------------------------------------------------------------------------//windowサイズ 閉じた時

global proc yk_DisplayEditor_hsize(int $x , int $flag)
{

int $count = 0;
string $name[];
if($x == 1){  //----赤
    $name = {"1" , "22" , "2" , "3" , "4" , "5"};
}
else if($x == 2){  //----緑
    $name = {"6" , "7" , "8" , "23" , "9" , "10" , "11" , "12"};
}
else if($x == 3){  //----青
    $name = {"13" , "14" , "15" , "24" , "16" , "17" , "18" , "19" , "20" , "25" ,"21"};
}
string $v;
for($v in $name){
    if($v == "8" || $v == "11"){  //----強さバー の時はbuttonでないので処理を分ける
        if(`floatSlider -q -vis ("yk_DisplayEditor_SET" + $v)`){
            $count++;
        }
        continue;
    }
    if(`button -q -vis ("yk_DisplayEditor_SET" + $v)`){
        $count++;
    }
}
int $i;
for($i = 1 ; $i <= 25 ; $i++){
    menuItem -e -en $flag ("yk_DisplayEditor_ITEM" + $i);   //----フレームを閉じた時、レイアウトを操作させないように
}
int $win = `window -q -h yk_DisplayEditor_WINDOW`; //----高さのサイズ
if($flag == 0){
    window -e -h ($win - (4 + (22 * $count))) yk_DisplayEditor_WINDOW;
}
else{
    window -e -h ($win + (4 + (22 * $count))) yk_DisplayEditor_WINDOW;
}

}

//------------------------------------------------------------------------------//レイアウトオフ

global proc yk_DisplayEditor_Layout(int $No)
{

if((`menuItem -q -cb ("yk_DisplayEditor_ITEM" + $No)`) == 1){
    if($No == 8 || $No == 11){
        floatSlider -e -vis 1 ("yk_DisplayEditor_SET" + $No);
    }
    else{
        button -e -vis 1 ("yk_DisplayEditor_SET" + $No);
    }
    window -e -h ((`window -q -h yk_DisplayEditor_WINDOW`) + 22) yk_DisplayEditor_WINDOW;
}
else{
    if($No == 8 || $No == 11){
        floatSlider -e -vis 0 ("yk_DisplayEditor_SET" + $No);
    }
    else{
        button -e -vis 0 ("yk_DisplayEditor_SET" + $No);
    }
    window -e -h ((`window -q -h yk_DisplayEditor_WINDOW`) - 22) yk_DisplayEditor_WINDOW;
}
yk_DisplayEditor_Save();  //----セーブ

}

//----------------------------------------------------------------//レイアウトReset

global proc yk_DisplayEditor_Reset()
{

frameLayout -e -cl 0 yk_DisplayEditor_FRAME1;
frameLayout -e -cl 0 yk_DisplayEditor_FRAME2;
frameLayout -e -cl 0 yk_DisplayEditor_FRAME3;

int $No;
for($No = 1 ; $No <= 25 ; $No++){  //----項目数分を書く
    menuItem -e -en 1 -cb 1 ("yk_DisplayEditor_ITEM" + $No);
    if($No == 8 || $No == 11){
        floatSlider -e -vis 1 ("yk_DisplayEditor_SET" + $No);
    }
    else{
        button -e -vis 1 ("yk_DisplayEditor_SET" + $No);
    }
}
window -e -wh 113 651 yk_DisplayEditor_WINDOW;

yk_DisplayEditor_Save();  //----セーブ

}

//------------------------------------------------------------------------------//セーブ

global proc yk_DisplayEditor_Save()
{

string $myDir = `internalVar -userScriptDir`;
int $file = `fopen ($myDir + "/" + "yk_DisplayEditor_5" + ".txt") "w"`;
fopen ($myDir + "/" + "yk_DisplayEditor_5" + ".txt") "a";
fprint $file ((`text -q -l yk_DisplayEditor_TEXT1`) + ",");  //----初めに合計を出力

int $i;
for($i = 1 ; $i <= 25 ; $i++){
    if((`menuItem -q -cb ("yk_DisplayEditor_ITEM" + $i)`) == 1){
        fprint $file ($i + ",");  //----Noを出力
    }
    else{
        fprint $file ("x,");  //----オフの場合は　X　を出力
    }
}
int $window_h = `window -q -h yk_DisplayEditor_WINDOW`;
fprint $file ($window_h);  //----最後にwindowサイズを出力

fclose $file;
}

//------------------------------------------------------------------------------//ロード

global proc yk_DisplayEditor_Load()
{

int $file , $No;
string $buffer[];
string $myDir = `internalVar -userScriptDir`;
if(`filetest -f ($myDir + "/yk_DisplayEditor_5.txt")` == 1){
    $file = `fopen ($myDir + "/" + "/yk_DisplayEditor_5" + ".txt") "r"`;
    string $save_data = `fread $file $save_data`;
    int $buffer_size = `tokenize $save_data "," $buffer`;
}
else{
    $buffer[26] = 651;  //----セーブデータなければ高さはデフォ値にする
}
for($No = 1 ; $No <= 26 ; $No++){  //----項目数分を書く
    if($buffer[$No] == "x"){
        menuItem -e -cb 0 ("yk_DisplayEditor_ITEM" + $No);
        if($No == 8 || $No == 11){
            floatSlider -e -vis 0 ("yk_DisplayEditor_SET" + $No);
        }
        else{
            button -e -vis 0 ("yk_DisplayEditor_SET" + $No);
        }
    }
}

int $h = $buffer[26];
window -e -h $h yk_DisplayEditor_WINDOW;

fclose $file;

}

//------------------------------------------------------------------------------//2point Distance

global proc string CyDisplayEditor_2p_Distance()

{
    float $PD_coord1[3] , $PD_coord2[3] , $PD_distance;
    string $currentSel[] = `ls -sl -fl`;
    int $selSize = `size $currentSel`;
    string $PD_points[] = `filterExpand -ex true -sm 31`;
    string $unit;

    if (size($PD_points) != 2)
    {
        headsUpDisplay -e -l "頂点を2つ選択してください" yk_DisplayEditor_DistanceHUD1;
        return $selSize;
    }
    else{
        $PD_coord1 = `pointPosition $PD_points[0]`;
        $PD_coord2 = `pointPosition $PD_points[1]`;
        $PD_distance = sqrt( (($PD_coord1[0] - $PD_coord2[0]) * ($PD_coord1[0] - $PD_coord2[0])) + (($PD_coord1[1] - $PD_coord2[1]) * ($PD_coord1[1] - $PD_coord2[1]))+(($PD_coord1[2] - $PD_coord2[2]) * ($PD_coord1[2] - $PD_coord2[2])));
        if(`currentUnit -query -linear` == "cm"){
            headsUpDisplay -e -l "2point Distance (センチメートル) : " yk_DisplayEditor_DistanceHUD1;//----単位を取得
            $unit = "cm";
        }
        else if(`currentUnit -query -linear` == "m"){
            headsUpDisplay -e -l "2point Distance (メートル) : " yk_DisplayEditor_DistanceHUD1;
            $unit = "m";
        }
        else{
            headsUpDisplay -e -l "2point Distance : " yk_DisplayEditor_DistanceHUD1;
            $unit = "";
        }
        string $size = $PD_distance;
        return ($size + " " + $unit);
    }
}

//------------------------------------------------------------------------------//

global proc CyDisplayEditor()
{

if(`window -q -ex yk_DisplayEditor_WINDOW`){deleteUI yk_DisplayEditor_WINDOW;}

window -t "CyDisplayEditor" -toolbox 1 -s 0 -menuBar true yk_DisplayEditor_WINDOW;

///////////////////////////////////////////////////////////////////////////////////////
menu -l "Layout Customize";
menuItem -l "Default"        -cb on -c "yk_DisplayEditor_Layout(1)"  yk_DisplayEditor_ITEM1;
menuItem -l "Texture"        -cb on -c "yk_DisplayEditor_Layout(22)" yk_DisplayEditor_ITEM22;
menuItem -l "Vertex Color"   -cb on -c "yk_DisplayEditor_Layout(2)"  yk_DisplayEditor_ITEM2;
menuItem -l "Texture + VC"   -cb on -c "yk_DisplayEditor_Layout(3)"  yk_DisplayEditor_ITEM3;
menuItem -l "Smooth Shaded"  -cb on -c "yk_DisplayEditor_Layout(4)"  yk_DisplayEditor_ITEM4;
menuItem -l "Wirefram"       -cb on -c "yk_DisplayEditor_Layout(5)"  yk_DisplayEditor_ITEM5;
menuItem -divider true;
menuItem -l "Polygon Border" -cb on -c "yk_DisplayEditor_Layout(6)"  yk_DisplayEditor_ITEM6;
menuItem -l "UV Border"      -cb on -c "yk_DisplayEditor_Layout(7)"  yk_DisplayEditor_ITEM7;
menuItem -l "Hard Edge"      -cb on -c "yk_DisplayEditor_Layout(23)" yk_DisplayEditor_ITEM23;
menuItem -l "--Border Width" -cb on -c "yk_DisplayEditor_Layout(8)"  yk_DisplayEditor_ITEM8;
menuItem -l "Vertex  Normal" -cb on -c "yk_DisplayEditor_Layout(9)"  yk_DisplayEditor_ITEM9;
menuItem -l "Face  Normal"   -cb on -c "yk_DisplayEditor_Layout(10)" yk_DisplayEditor_ITEM10;
menuItem -l "--Normal Size"  -cb on -c "yk_DisplayEditor_Layout(11)" yk_DisplayEditor_ITEM11;
menuItem -l "Buckface Off"   -cb on -c "yk_DisplayEditor_Layout(12)" yk_DisplayEditor_ITEM12;
menuItem -divider true;
menuItem -l "Isolate"  -cb on -c "yk_DisplayEditor_Layout(13)" yk_DisplayEditor_ITEM13;
menuItem -l "Over Wirefram"  -cb on -c "yk_DisplayEditor_Layout(14)" yk_DisplayEditor_ITEM14;
menuItem -l "UVShell Select" -cb on -c "yk_DisplayEditor_Layout(15)" yk_DisplayEditor_ITEM15;
menuItem -l "Joints"         -cb on -c "yk_DisplayEditor_Layout(24)" yk_DisplayEditor_ITEM24;
menuItem -l "Triangle"       -cb on -c "yk_DisplayEditor_Layout(16)" yk_DisplayEditor_ITEM16;
menuItem -l "Instance"       -cb on -c "yk_DisplayEditor_Layout(17)" yk_DisplayEditor_ITEM17;
menuItem -l "Bg Color"       -cb on -c "yk_DisplayEditor_Layout(18)" yk_DisplayEditor_ITEM18;
menuItem -l "UI Elements"    -cb on -c "yk_DisplayEditor_Layout(19)" yk_DisplayEditor_ITEM19;
menuItem -l "X ray"          -cb on -c "yk_DisplayEditor_Layout(20)" yk_DisplayEditor_ITEM20;
menuItem -l "2point Distance"-cb on -c "yk_DisplayEditor_Layout(20)" yk_DisplayEditor_ITEM25;
menuItem -l "Grid"           -cb on -c "yk_DisplayEditor_Layout(21)" yk_DisplayEditor_ITEM21;
menuItem -divider true;
menuItem -l "Layout Reset" -c "yk_DisplayEditor_Reset()";
///////////////////////////////////////////////////////////////////////////////////////

columnLayout -adjustableColumn true ;
separator -height 2 -style "none";

frameLayout -w 80 -l "Material" -collapsable 1 -pcc "yk_DisplayEditor_hsize(1 , 0)" -pec "yk_DisplayEditor_hsize(1 , 1)" yk_DisplayEditor_FRAME1;  //----赤
columnLayout;
button -w 105 -h 22 -l "Default"       -c "yk_DisplayEditor_Cmds(1 , \"red\" , \"yk_DisplayEditor_SET1\")" -bgc 1 0.8 0.8 -ann "デフォルト表示にします" yk_DisplayEditor_SET1;
button -w 105 -h 22 -l "Texture"       -c "yk_DisplayEditor_Cmds(22 , \"red\" , \"yk_DisplayEditor_SET22\")" -bgc 1 0.8 0.8 -ann "テクスチャ表示にします" yk_DisplayEditor_SET22;//----追加2017/05/15
button -w 105 -h 22 -l "Vertex Color"  -c "yk_DisplayEditor_Cmds(2 , \"red\" , \"yk_DisplayEditor_SET2\")" -bgc 1 0.8 0.8 -ann "頂点カラー表示にします" yk_DisplayEditor_SET2;
button -w 105 -h 22 -l "Texture + VC"  -c "yk_DisplayEditor_Cmds(3 , \"red\" , \"yk_DisplayEditor_SET3\")" -bgc 1 0.8 0.8 -ann "テクスチャ&頂点カラー表示にします" yk_DisplayEditor_SET3;
button -w 105 -h 22 -l "Smooth Shaded" -c "yk_DisplayEditor_Cmds(4 , \"red\" , \"yk_DisplayEditor_SET4\")" -bgc 1 0.8 0.8 -ann "スムースシェーディング表示にします" yk_DisplayEditor_SET4;
button -w 105 -h 22 -l "Wireframe"     -c "yk_DisplayEditor_Cmds(5 , \"red\" , \"yk_DisplayEditor_SET5\")" -bgc 1 0.8 0.8 -ann "ワイヤーフレーム表示にします" yk_DisplayEditor_SET5;
setParent ..;setParent ..;

frameLayout -w 80 -l "Geometry" -collapsable 1 -pcc "yk_DisplayEditor_hsize(2 , 0)" -pec "yk_DisplayEditor_hsize(2 , 1)" yk_DisplayEditor_FRAME2;  //----緑
columnLayout;
button -w 105 -h 22 -l "Polygon Border" -c "yk_DisplayEditor_Cmds(6 , \"green\" , \"yk_DisplayEditor_SET6\")" -bgc 0.8 1 0.8 -ann "ポリゴンのボーダーラインを表示します" yk_DisplayEditor_SET6;
button -w 105 -h 22 -l "UV Border"      -c "yk_DisplayEditor_Cmds(7 , \"green\" , \"yk_DisplayEditor_SET7\")" -bgc 0.8 1 0.8 -ann "UVのボーダーラインを表示します" yk_DisplayEditor_SET7;
button -w 105 -h 22 -l "Hard Edge"       -c "yk_DisplayEditor_Cmds(23 , \"green\" , \"yk_DisplayEditor_SET23\")" -bgc 0.8 1 0.8 -ann "ハードエッジを表示します" yk_DisplayEditor_SET23;
floatSlider -w 100 -h 22 -min 0 -max 10 -v 2 -cc"yk_DisplayEditor_Cmds(8 , \"green\" , \"yk_DisplayEditor_SET8\")" yk_DisplayEditor_SET8; //----ボーダーの強さ
button -w 105 -h 22 -l "Vertex Normal"  -c "yk_DisplayEditor_Cmds(9 , \"green\" , \"yk_DisplayEditor_SET9\")" -bgc 0.8 1 0.8 -ann "バーテックスの法線を表示します" yk_DisplayEditor_SET9;
button -w 105 -h 22 -l "Face Normal" -c "yk_DisplayEditor_Cmds(10 , \"green\" , \"yk_DisplayEditor_SET10\")" -bgc 0.8 1 0.8 -ann "フェースの法線を表示します" yk_DisplayEditor_SET10;
floatSlider -w 100 -h 22 -min 0 -max 3 -v 0.6 -cc"yk_DisplayEditor_Cmds(11 , \"green\" , \"yk_DisplayEditor_SET11\")" yk_DisplayEditor_SET11; //----法線の強さ
button -w 105 -h 22 -l "Buckface Off"   -c "yk_DisplayEditor_Cmds(12 , \"green\" , \"yk_DisplayEditor_SET12\")" -bgc 0.8 1 0.8 -ann "ポリゴンの裏面を非表示にします" yk_DisplayEditor_SET12;
setParent ..;setParent ..;

frameLayout -w 80 -l "View" -collapsable 1 -pcc "yk_DisplayEditor_hsize(3 , 0)" -pec "yk_DisplayEditor_hsize(3 , 1)" yk_DisplayEditor_FRAME3;  //----青
columnLayout;
button -w 105 -h 22 -l "Isolate"  -c "yk_DisplayEditor_Cmds(13 , \"blue\" , \"yk_DisplayEditor_SET13\")" -bgc 0.8 0.8 1 -ann "選択したモデル（メッシュ）のみ表示します" yk_DisplayEditor_SET13;
button -w 105 -h 22 -l "Over Wirefram"  -c "yk_DisplayEditor_Cmds(14 , \"blue\" , \"yk_DisplayEditor_SET14\")" -bgc 0.8 0.8 1 -ann "ワイヤーフレームを重ねて表示します" yk_DisplayEditor_SET14;
button -w 105 -h 22 -l "UVShell Select" -c "SelectUVShell" -bgc 0.8 0.8 1 -ann "UVシェルを選択します" yk_DisplayEditor_SET15;
button -w 105 -h 22 -l "Joints"         -c "yk_DisplayEditor_Cmds(24 , \"blue\" , \"yk_DisplayEditor_SET24\")" -bgc 0.8 0.8 1 -ann "ジョイントを表示します" yk_DisplayEditor_SET24;
button -w 105 -h 22 -l "Triangle"       -c "yk_DisplayEditor_Cmds(16 , \"blue\" , \"yk_DisplayEditor_SET16\")" -bgc 0.8 0.8 1 -ann "四角ポリゴンに三角網を表示します" yk_DisplayEditor_SET16;
button -w 105 -h 22 -l "Instance"       -c "yk_DisplayEditor_Cmds(17 , \"blue\" , \"yk_DisplayEditor_SET17\")" -bgc 0.8 0.8 1 -ann "インスタンスモデルのワイヤーフレームが紫色になります" yk_DisplayEditor_SET17;
button -w 105 -h 22 -l "Bg Color"       -c "yk_DisplayEditor_Cmds(18 , \"blue\" , \"yk_DisplayEditor_SET18\")" -bgc 0.8 0.8 1 -ann "バックカラーをデフォルト→ブラック→グレー→ホワイトと循環します" yk_DisplayEditor_SET18;
button -w 105 -h 22 -l "UI Elements"    -c "yk_DisplayEditor_Cmds(19 , \"blue\" , \"yk_DisplayEditor_SET19\")" -bgc 0.8 0.8 1 -ann "UIエレメントを切り換えます" yk_DisplayEditor_SET19;
button -w 105 -h 22 -l "X Ray"          -c "yk_DisplayEditor_Cmds(20 , \"blue\" , \"yk_DisplayEditor_SET20\")" -bgc 0.8 0.8 1 -ann "半透明表示にします" yk_DisplayEditor_SET20;
button -w 105 -h 22 -l "2point Distance"-c "yk_DisplayEditor_Cmds(25 , \"blue\" , \"yk_DisplayEditor_SET25\")" -bgc 0.8 0.8 1 -ann "2頂点間の距離を表示します" yk_DisplayEditor_SET25;
button -w 105 -h 22 -l "Grid"           -c "yk_DisplayEditor_Cmds(21 , \"blue\" , \"yk_DisplayEditor_SET21\")" -bgc 0.8 0.8 1 -ann "グリッドを1(単位)→10→50→100→オフと循環します" yk_DisplayEditor_SET21;
setParent ..;setParent ..;

separator -height 20 -style "none";

text -l "25" yk_DisplayEditor_TEXT1;
text -l "1"  yk_DisplayEditor_TEXT2;  //----Bg Color
text -l "1"  yk_DisplayEditor_TEXT3;  //----UI
text -l "1"  yk_DisplayEditor_TEXT4;  //----Grid
setParent ..;

showWindow yk_DisplayEditor_WINDOW;
window -e -wh 113 651 yk_DisplayEditor_WINDOW;

yk_DisplayEditor_Load();

}

